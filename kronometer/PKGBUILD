pkgname=kronometer
pkgver=2.2.2
pkgrel=1
pkgdesc="A simple chronometer application"
arch=('x86_64')
url="https://userbase.kde.org/Kronometer"
license=('GPL2')
depends=('kxmlgui' 'hicolor-icon-theme')
conflicts=('kronometer-git')
makedepends=('extra-cmake-modules' 'kdoctools')
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz)
sha256sums=('52bc998d8d8cde10da331a065d7016b0946123a5ea1ab82f6986c6e7f10c4e90')
 
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
