# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=libkdegames
pkgver=19.08.2
pkgrel=3
pkgdesc="Shared libraries for KDE games"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdegames/libkdegames'
license=('GPL' 'LGPL' 'FDL')
depends=('karchive' 'kbookmarks' 'kcodecs' 'kcompletion' 'kconfig'
         'kconfigwidgets' 'kcoreaddons' 'kcrash' 'kdbusaddons' 'kdeclarative'
         'kdnssd' 'kjobwidgets' 'kglobalaccel' 'kguiaddons' 'ki18n'
         'kiconthemes' 'kio' 'kitemviews' 'knewstuff' 'kservice'
         'ktextwidgets' 'kwidgetsaddons' 'kxmlgui' 'libsndfile' 'openal'
         'qt5-base' 'qt5-declarative' 'qt5-svg')
makedepends=('cmake' 'extra-cmake-modules')
groups=("kde" "kdegames" "kde-uninstall")
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('83456cec44502a1f79c0be00c983090e32fd8aea5fec1461fbfbd37b5f8866ac'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
