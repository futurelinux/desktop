pkgname=nano
pkgver=4.0
_mainver=4
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="https://www.nano-editor.org"
groups=('base')
depends=('file' 'ncurses' 'sh')
source=(https://www.nano-editor.org/dist/v${_mainver}/nano-${pkgver}.tar.xz)
backup=(etc/nanorc)
md5sums=('2de3e8a60ecf09649e316e89be2aac65')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-color \
              --enable-nanorc \
	      --enable-multibuffer
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc
}
