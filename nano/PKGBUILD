pkgname=nano
pkgver=4.9.2
_mainver=4
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="https://www.nano-editor.org"
groups=('base')
depends=('file' 'ncurses' 'sh')
source=(https://www.nano-editor.org/dist/v${_mainver}/${pkgname}-${pkgver}.tar.xz)
backup=(etc/nanorc)
sha256sums=('d8a25eea942ecee2d57b8e037eb4b28f030f818b78773b8fcb994ed5835d2ef6')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-color \
              --enable-nanorc \
	      --enable-multibuffer
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc
}
