pkgname=octopi
pkgver=0.10.0
pkgrel=3
pkgdesc="Octopi, a powerful Pacman frontend using Qt libs"
url="https://octopiproject.wordpress.com/"
arch=('x86_64')
license=('GPL2')
depends=(qtermwidget qt5-quickcontrols qt5-declarative pacman pacmanlogviewer gist chaser
         alpm_octopi_utils kdesu knotifications)
makedepends=(pacman-contrib qt5-tools)
provides=(octopi-notifier)
replaces=(octopi-notifier)
conflicts=(octopi-notifier)
categories=('system') 
source=("git+https://github.com/aarnt/octopi.git") # v0.9.0^1192
md5sums=('SKIP')

prepare(){
    cd "${srcdir}/${pkgname}"

    # patch .desktop files
    sed -i 's/Categories=GNOME;GTK;System;/Categories=System;Tools;/g' octopi.desktop notifier/octopi-notifier.desktop cachecleaner/octopi-cachecleaner.desktop
    sed -i 's/\/\/#define NO_GTK_STYLE/#define NO_GTK_STYLE/g' src/main.cpp notifier/main.cpp
    sed -i 's/Icon=octopi/Icon=octopi-notifier/g' notifier/octopi-notifier.desktop

    # enable the kstatus switch
    sed -e 's|\#KSTATUS|KSTATUS|' -i notifier/octopi-notifier.pro

    # edit octopi.service
    sed -e 's,speedup-octopi.sh,speedup-octopi,g' -i speedup/octopi.service
}

build() {
   cd "${srcdir}/${pkgname}"
   /usr/lib/qt5/bin/qmake
   make

   # Modules
   for mod in helper notifier repoeditor cachecleaner sudo; do
     msg "building $mod"
     pushd "$mod"
       qmake-qt5
       make
     popd
   done
}

package_octopi() {
   cd ${srcdir}/${pkgname}
   make INSTALL_ROOT=${pkgdir} install

   # Modules
   for mod in repoeditor cachecleaner sudo helper notifier; do
   msg "install $mod"
       make -C ${mod} INSTALL_ROOT=${pkgdir} install
   done

   install -Dm644 resources/images/octopi_green.png ${pkgdir}/usr/share/pixmaps/octopi.png
   install -Dm644 resources/images/octopi_red.png ${pkgdir}/usr/share/pixmaps/octopi-notifier.png

   # Speedup
   install -Dm 644 speedup/octopi.service ${pkgdir}/usr/lib/systemd/system/octopi.service
   install -Dm 755 speedup/speedup-octopi.sh ${pkgdir}/usr/lib/octopi/speedup-octopi
   ln -s /usr/lib/octopi/speedup-octopi $pkgdir/usr/bin/speedup-octopi
}
