# Contributions from Arch:
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=geoclue
pkgver=2.5.5
pkgrel=1
pkgdesc="Modular geoinformation service built on the D-Bus messaging system"
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/GeoClue/"
license=(LGPL)
depends=(libsoup json-glib modemmanager avahi libnotify)
makedepends=(systemd gobject-introspection git vala meson gtk-doc)
backup=(etc/geoclue/geoclue.conf)
_commit=9519d67c7a9a0e97a75115f9ff99d4fb0bb81b21  # tags/2.5.5^0
source=("git+https://gitlab.freedesktop.org/geoclue/geoclue.git#commit=$_commit"
        0001-config-Make-the-Mozilla-API-key-configurable.patch
        0002-config-Improve-config-lookup-error-messages.patch
        0003-config-Clear-error-after-submission-URL-lookup-fails.patch
        0004-config-Fall-back-to-a-default-submission-nick-if-loo.patch)
sha256sums=('SKIP'
            '4b5540f55fee97cb275b781d7f80a56685025fadfd3a79fe5244d79b2635dc6f'
            '31d69df4f6da470c06cd03f3d5f40345902c246322437d77d618b0fa7659620e'
            '9146ad2ff101602249c09dd3bc688bb0a2f151dcd646200d329359e8c687adc3'
            'ca496af7d50e138e17cd9a69a16f729a7c5ae731f4dc93f32027be6852c7cdce')

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact totte@chakralinux.org for
# more information.
_mozilla_api_key=e05d56db0a694edc8b5aaebda3f2db6a

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git apply -3 ../*.patch
}

build() {
  chakra-meson $pkgname build \
    -D dbus-srv-user=geoclue \
    -D dbus-sys-dir=/usr/share/dbus-1/system.d \
    -D mozilla-api-key="$_mozilla_api_key"
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  echo 'u geoclue - "Geoinformation service" /var/lib/geoclue' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  echo 'd /var/lib/geoclue 0755 geoclue geoclue' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
