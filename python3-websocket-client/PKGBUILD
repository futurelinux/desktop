
pkgname='python3-websocket-client'
_distname=websocket_client
pkgver=0.57.0
pkgrel=1
pkgdesc="WebSocket client library for Python"
arch=(x86_64)
url="https://github.com/websocket-client/websocket-client"
license=('LGPL')
makedepends=('python3-setuptools' 'python3-six')
depends=('python3-six')
changelog=$pkgname.changelog
source=("https://files.pythonhosted.org/packages/8b/0f/52de51b9b450ed52694208ab952d5af6ebbcbce7f166a48784095d930d8c/${_distname}-${pkgver}.tar.gz")
sha256sums=('d735b91d6d1692a6a181f2a8c9e0238e5f6373356f561bb9dc4c7af36f452010')

prepare() {
  sed -i "s/'backports.ssl_match_hostname'//" ${srcdir}/${_distname}-${pkgver}/setup.py
}

build() {
  cd ${_distname}-${pkgver}
  python3 setup.py build
}

check() {
  cd ${_distname}-${pkgver}
  python3 setup.py test
}

package() {
  cd ${_distname}-${pkgver}
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
