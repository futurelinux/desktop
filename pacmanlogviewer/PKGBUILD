# Contributions from Arch:
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>
 
pkgname=pacmanlogviewer
pkgver=1.4.1
pkgrel=2
pkgdesc="Inspect pacman log files"
arch=('x86_64')
url='https://opendesktop.org/content/show.php?content=150484'
license=('GPL')
depends=('qt5-base' 'hicolor-icon-theme')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/gcala/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('b8f870e2f34d285b481bf862e96b582c3a97fd2da7bd977915b1973fcd5ab0f8')

prepare() {
  mkdir -p build
}
 
build() {
  cd build
  cmake ../"${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_WITH_QT5=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
} 
