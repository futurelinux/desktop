pkgname=mpc
pkgver=0.33
pkgrel=1
pkgdesc="Minimalist command line interface to MPD"
arch=('x86_64')
url='https://www.musicpd.org/clients/mpc/'
license=('GPL2')
depends=('libmpdclient')
makedepends=('meson' 'python3-sphinx')
options=('!emptydirs')
source=("http://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4f40ccbe18f5095437283cfc525a97815e983cbfd3a29e48ff610fa4f1bf1296')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    chakra-meson . build
    ninja -C build
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    DESTDIR="${pkgdir}" ninja -C build install

    # Install bash completion file
    install -Dm644 contrib/mpc-completion.bash "${pkgdir}/usr/share/bash-completion/completions/mpc"
}
 
