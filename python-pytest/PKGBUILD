pkgbase=python-pytest
pkgname=('python2-pytest' 'python3-pytest')
pkgver=5.4.0
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('x86_64')
license=('MIT')
url="http://pytest.org/"
makedepends=('python2-setuptools' 'python3-setuptools'
             'python2-py'         'python3-py'
             'python2-pluggy'     'python3-pluggy'
             'python2-attrs'      'python3-attrs'
             'python2-funcsigs')
checkdepends=('lsof'
              'python3-nose'         'python2-nose'
              'python3-mock'         'python2-mock'
              'python3-tox'          'python2-tox'
              'python3-yaml'         'python2-yaml'
              'python3-pytest-xdist' 'python2-pytest-xdist'
              'python3-twisted'      'python2-twisted'
              'python3-requests'     'python2-requests'
              'python3-hypothesis'   'python2-hypothesis'
              'python3-pluggy'       'python2-pluggy')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('1042f537e73752257ed982470f4abd09e1b7031c5c4b428a319684008dac136ac68a2fb8b663140b2e312dff737bf178438d3c122e800f07bd10233417f73a57')

prepare() {
  cp -a pytest-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pytest-$pkgver
  python3 setup.py build

  cd "$srcdir"/pytest-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pytest-$pkgver
  python3 setup.py test

#  cd "$srcdir"/pytest-$pkgver-py2
#  python2 setup.py test
}

package_python3-pytest() {
  depends=('python3-py' 'python3-setuptools' 'python3-pluggy' 'python3-attrs')

  cd pytest-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest() {
  depends=('python2-py' 'python2-setuptools' 'python2-pluggy' 'python2-attrs' 'python2-funcsigs')

  cd pytest-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/py.test{,2}
  mv "$pkgdir"/usr/bin/pytest{,2}
}
