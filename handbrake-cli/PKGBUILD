# contributions from ArchLinux: https://www.archlinux.org/packages/community/x86_64/handbrake-cli/

pkgname=handbrake-cli
pkgver=1.2.2
pkgrel=1
pkgdesc="Multithreaded video transcoder (CLI)"
arch=('x86_64')
url="https://handbrake.fr/"
license=('GPL')
depends=('bzip2' 'gcc-libs' 'zlib' 'xz' 'fribidi' 'libass' 'lame' 'libxml2'
         'libtheora' 'libsamplerate' 'ffmpeg' 'libx264' 'x264' 'x265' 'libvpx'
         'libdvdnav' 'jansson' 'speex')
makedepends=('intltool' 'python2' 'nasm' 'wget' 'libnotify' 'gst-plugins-base' 'dbus-glib' 'cmake')
source=("https://download2.handbrake.fr/$pkgver/HandBrake-$pkgver-source.tar.bz2")
categories=('multimedia')
sha256sums=('df6816f517d60ae8a6626aa731821af2d1966c155fa53b2b9a06c47f3c565e4c')

prepare() {
  cd HandBrake-$pkgver

  # Use more system libs
  # Bundled libbluray is kept because it is patched locally
  # Our ffmpeg is a liitle old so keep using bundled version
  for _lib in libvpx libdvdnav libdvdread; do
    sed -i "/MODULES += contrib\/$_lib/d" make/include/main.defs
  done
}

build() {
  cd HandBrake-$pkgver
  
  ./configure --prefix=/usr \
              --force \
              --disable-gtk \
              --disable-gtk-update-checks
  cd build
  make
}

package() {
  cd HandBrake-$pkgver/build
  
  install -D HandBrakeCLI "$pkgdir/usr/bin/handbrake-cli"
}
