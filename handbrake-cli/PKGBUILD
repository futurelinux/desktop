# contributions from ArchLinux: https://www.archlinux.org/packages/community/x86_64/handbrake-cli/

pkgname=handbrake-cli
pkgver=1.3.0
pkgrel=1
pkgdesc="Multithreaded video transcoder (CLI)"
arch=('x86_64')
url="https://handbrake.fr/"
license=('GPL')
depends=('libxml2' 'libass' 'libvorbis' 'opus' 'speex' 'libtheora' 'lame' 'x264' 'jansson' 'libvpx' 'libva' 'numactl'
         'bzip2' 'gcc-libs' 'zlib' 'xz' 'libdvdnav' 'libdvdread')
makedepends=('intltool' 'python3' 'nasm' 'wget' 'cmake' 'meson')
optdepends=('libdvdcss: for decoding encrypted DVDs')
source=("https://download2.handbrake.fr/$pkgver/HandBrake-$pkgver-source.tar.bz2")
sha256sums=('a9a82eb5ca04a793705b3d7d11cefa29946694eeb13b40161446aaca35b31d96')

prepare() {
  cd HandBrake-$pkgver

  # Use more system libs
  # Bundled libbluray is kept because it is patched locally
  # Our ffmpeg is a liitle old so keep using bundled version
  for _lib in libvpx libdvdnav libdvdread; do
    sed -i "/MODULES += contrib\/$_lib/d" make/include/main.defs
  done
}

build() {
  cd HandBrake-$pkgver
  
  ./configure --prefix=/usr \
              --force \
              --disable-gtk \
              --disable-gtk-update-checks \
              --enable-qsv
  cd build
  make
}

package() {
  cd HandBrake-$pkgver/build
  
  install -D HandBrakeCLI "$pkgdir/usr/bin/handbrake-cli"
}
