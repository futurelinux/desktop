# Contributions from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=plasma5-applets-latte-dock
_pkgname=latte-dock
pkgver=0.9.4
pkgrel=1
pkgdesc='A dock based on Plasma Frameworks'
arch=(x86_64)
url='https://store.kde.org/p/1169519/'
license=(GPL)
depends=(kcrash kglobalaccel knewstuff kwayland plasma-framework libksysguard)
makedepends=(extra-cmake-modules python3)
conflicts=(plasma5-applets-nowdock)
replaces=(plasma5-applets-nowdock)
source=("https://download.kde.org/stable/$_pkgname/$_pkgname-$pkgver.tar.xz")
sha256sums=('a1e706b25ced6a68148a4db92d568c6a0a0494aba34525b1fea183c0830e34ee')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
