pkgname=freecell-solver
pkgver=5.14.0
pkgrel=1
pkgdesc="A program that automatically solves layouts of Freecell and similar variants of Card Solitaire"
arch=(x86_64)
url="http://fc-solve.shlomifish.org"
license=(MIT)
depends=(bash)
makedepends=(cmake gperf perl-path-tiny perl-template-toolkit python3-random2 python3-six python3-pysol_cards)
optdepends=('python3-random2: for the Python scripts'
            'python3-six: for the Python scripts'
            'python3-pysol_cards: for the Python interface')
source=(https://fc-solve.shlomifish.org/downloads/fc-solve/$pkgname-$pkgver.tar.xz)
sha256sums=('b09e7394181fcd972436fbbcd2192a9085b8924b70c892ad88b9ad95a7abee12')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFCS_WITH_TEST_SUITE=OFF \
    -DBUILD_STATIC_LIBRARY=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

