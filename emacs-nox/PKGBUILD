pkgname=emacs-nox
pkgver=27.1
pkgrel=1
pkgdesc='The Emacs Editor, without X11 support'
arch=('x86_64')
url="https://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus' 'ncurses' 'perl' 'gnutls' 'gpm' 'jansson')
provides=('emacs')
conflicts=('emacs' 'emacs-cvs')
options=('docs')
source=(ftp://ftp.gnu.org/gnu/emacs/emacs-${pkgver}.tar.xz)
md5sums=('f1f25356b68ec69caccd41d95988d23d')

build() {
  cd $srcdir/emacs-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-x --without-sound --with-modules
  make
}

package() {
  cd $srcdir/emacs-$pkgver
  make DESTDIR=${pkgdir} install

  #remove conflict with ctags package
  mv $pkgdir/usr/bin/{ctags,ctags.emacs}
  mv $pkgdir/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}

  #fix user/root permissions on usr/share files
  find $pkgdir/usr/share/emacs/$pkgver -exec chown root.root {} \;
  #remove .desktop file and icons
  rm -rf $pkgdir/usr/share/{applications,icons}
}
