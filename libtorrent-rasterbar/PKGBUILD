pkgname=libtorrent-rasterbar
pkgver=1.2.2
_pkgver=${pkgver//./_}
pkgrel=2
pkgdesc="A C++ library that aims to be a good alternative to all the other bittorrent implementations around"
url="https://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
depends=('boost-libs' 'python2' 'python3' 'geoip' 'websocketpp')
makedepends=('boost' 'cmake')
options=('!emptydirs')
source=(https://github.com/arvidn/libtorrent/archive/libtorrent-${_pkgver}/$pkgname-$pkgver.tar.gz)
sha256sums=('353793a234858e8dae9ee4a37d59c8c5297b718cc7ae3efe5798f004c0da6856')

prepare() {
  mkdir py2 py3
  cd libtorrent-libtorrent-${_pkgver}

  # Avoid depending on newer processors
  sed -i 's/-msse4.2//' configure.ac

  ./autotool.sh
}

_build() (
  cd py$1

  # FS#50745
  _boost="boost_python"
  if [ $1 -eq 3 ]; then _boost="boost_python3"; fi

  PYTHON=/usr/bin/python$1 \
  ../libtorrent-libtorrent-${_pkgver}/configure \
    --prefix=/usr \
    --enable-python-binding \
    --enable-examples \
    --disable-static \
    --with-libiconv \
    --with-boost-python=$_boost
)

build() {
  _build 2
  _build 3
}

package() {
  make -C py2 DESTDIR="$pkgdir" install
  make -C py3 DESTDIR="$pkgdir" install
  install -Dm644 libtorrent-libtorrent-${_pkgver}/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove most example binaries
  rm "$pkgdir"/usr/bin/{*_test,*_tester,simple_client,stats_counters}
}
