# Contributions from Arch:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ian Yang <doit.ian@gmail.com>

pkgname=google-glog
pkgver=0.4.0
pkgrel=1
pkgdesc="Logging library for C++"
arch=('x86_64')
license=('custom:BSD3')
url='https://github.com/google/glog'
depends=('gcc-libs')
source=("glog-$pkgver.tar.gz::https://github.com/google/glog/archive/v$pkgver.tar.gz")
sha256sums=('f28359aeba12f30d73d9e4711ef356dc842886968112162bc73002645139c39c')

build() {
  cd glog-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd glog-$pkgver

  make DESTDIR="$pkgdir" install

  # Lazy way of dealing with conflicting man and info pages...
  rm -rf "$pkgdir"/usr/share

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
