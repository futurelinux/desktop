# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=spectacle
pkgver=19.08.3
pkgrel=1
pkgdesc="KDE screenshot capture utility"
arch=(x86_64)
url='https://projects.kde.org/spectacle'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose knewstuff kwayland qt5-tools)
makedepends=(extra-cmake-modules kdoctools python3)
conflicts=(kdegraphics-ksnapshot kscreengenie)
replaces=(kdegraphics-ksnapshot kscreengenie)
groups=(kde-applications kdegraphics)
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('3717b7ebd314cac47c263c304c54c14b0115b15cfa4b87201f5ea27a2f4e9b8a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  cd ${srcdir}/${pkgname}-${pkgver}
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  # /usr/bin/qdbus is still Qt 4, set to use /usr/bin/qdbus-qt5 for now
  sed -i 's#Exec=qdbus org.kde.Spectacle#Exec=qdbus-qt5 org.kde.Spectacle#' "${pkgdir}/usr/share/applications/org.kde.spectacle.desktop"
}
