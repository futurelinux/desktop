pkgname=hplip
pkgver=3.19.1
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet."
arch=('x86_64')
url="https://hplipopensource.com"
license=('GPL2' 'custom')
depends=('python3' 'ghostscript' 'net-snmp' 'wget' 'foomatic-db-engine'
         #dependencies added by default to run the GUI and other services
         'python3-pyqt5' 'python3-gobject' 'cups' 'libusb' 'python3-dbus')
makedepends=('sane' 'rpcbind' )
optdepends=('sane: for scanner support'
            'python3-pillow: for commandline scanning support'
            'python3-reportlab: for pdf output in hp-scan'
            'rpcbind: for network support')
backup=('etc/hp/hplip.conf' 'etc/sane.d/dll.d/hpaio')
source=(https://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz{,.asc}
        disable_upgrade.patch
        0022-Add-include-cups-ppd.h-in-various-places-as-CUPS-2.2.patch
	0023-Fix-handling-of-unicode-filenames-in-sixext.py.patch
        0025-Remove-all-ImageProcessor-functionality-which-is-clo.patch)
sha512sums=('b1d663a3538a19ab2c195bc627c765b9dded047b0431f6fc2096a8a31b4efb5cf52c88ad9665a2d78893ba66c1c47ccf79c161965f308a27eeeb11b156b446c8'
            'SKIP'
	    'e1961372dd12788978eb7b2a5068c35da2bb879e0c89a4d2a1c5ca29a7e7784f9776459e305650c5ec0867743d675ba79b81c8fd3422d39f3fb65eae970c9d78'
            '22aeb5b851f78bc6bc62e0bc3da99fecaf42d7604af41e2f3343f8d3666541f7b06b7d1a7d0ddf24f1731ac7b12dfe582375a98e3b94dfa323d6ce954549ca67'
            '763949a0bc460dcc9faefc86f2a91cf342781bfce696ed0c3826758572dd03ac266bbeb7b6a4f9376ac298d7d3c9c4def42d94921a8e1d1695e39396e36d95ff'
            '8710e039626878270b8b7bc1569566274d935c84652d758e25ce8fe01c0f44d911148620bb494489e1238201c01f3ba255c19f7dc5c2ff0d45a5f2a79190286b')
validpgpkeys=('4ABA2F66DBD5A95894910E0673D770CDA59047B9') # HPLIP (HP Linux Imaging and Printing) <hplip@hp.com>

prepare() {
  cd $pkgname-$pkgver
  
  export PYTHON=python3
  
  find . -type f -exec sed -i 's~^#.*env python~#!/usr/bin/env python3~' {} +
  sed -i '1s|#!/usr/bin/python|#!/usr/bin/python3|' doctor.py setup.py makeuri.py logcapture.py base/magic.py
  sed -i '1s|#!/usr/bin/python|#!/usr/bin/python3|' ui/upgradeform.py uninstall.py upgrade.py config_usb_printer.py
  sed -i 's|python ./print.py|python3 ./print.py|' scan.py
  sed -i 's|python ./testpage.py|python3 ./testpage.py|' setup.py
  sed -i 's|python ./setup.py|python3 ./setup.py|' ui4/devmgr5.py ui4/nodevicesdialog.py 
  sed -i 's|python %HOME%|python3 %HOME%|' base/utils.py
  sed -i 's|python ./plugin.py|python3 ./plugin.py|' base/pkit.py
  sed -i 's|/usr/bin/python|/usr/bin/python3|' data/rules/hplip-printer@.service
  
  # disable insecure update - https://bugs.archlinux.org/task/38083
  patch -Np0 -i ${srcdir}/disable_upgrade.patch
  # add missing 'include <cups/ppd.h>' at various places
  patch -Np1 -i ${srcdir}/0022-Add-include-cups-ppd.h-in-various-places-as-CUPS-2.2.patch
  # fix some handling unicode file names FS#58412
  patch -Np1 -i ${srcdir}/0023-Fix-handling-of-unicode-filenames-in-sixext.py.patch
  # remove binary blob libImageProcessor and all linking - FS#59681
  patch -Np1 -i ${srcdir}/0025-Remove-all-ImageProcessor-functionality-which-is-clo.patch
    
  export AUTOMAKE='automake --foreign'
  autoreconf --force --install
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
	     --enable-qt5 \
             --disable-qt4 \
             --disable-foomatic-rip-hplip-install \
             --enable-foomatic-ppd-install \
             --enable-hpcups-install \
             --enable-new-hpcups \
             --enable-cups-ppd-install \
             --enable-cups-drv-install \
             --enable-hpijs-install \
             --enable-foomatic-drv-install \
             --enable-pp-build #--help

 make
}

package() {
  cd $pkgname-$pkgver
  make -j1 rulesdir=/usr/lib/udev/rules.d DESTDIR=$pkgdir install
  
  # remove config provided by sane and autostart of hp-daemon
  rm -rf $pkgdir/etc/{sane.d,xdg}
  install -dm755 ${pkgdir}/etc/sane.d/dll.d
  echo hpaio > ${pkgdir}/etc/sane.d/dll.d/hpaio
  
  # remove HAL .fdi file because HAL is no longer used
  rm -rf $pkgdir/usr/share/hal
 
  # remove rc script
  rm -rf $pkgdir/etc/init.d

  # add mixed license file
  install -Dt "${pkgdir}"/usr/share/licenses/${pkgname} -m644 COPYING
}

