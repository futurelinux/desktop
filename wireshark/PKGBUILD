pkgname=wireshark
pkgver=3.2.6
pkgrel=1
pkgdesc='A free network protocol analyzer for Unix/Linux and Windows'
arch=('x86_64')
license=('GPL2')
depends=('c-ares' 'libmaxminddb' 'krb5' 'libgcrypt' 'libcap' 'libpcap'
         'gnutls' 'glib2' 'lua52' 'libssh' 'libxml2' 'libnghttp2' 'snappy'
         'lz4' 'spandsp' 'sbc' 'bcg729' 'desktop-file-utils' 'qt5-multimedia' 'qt5-svg'
         'shared-mime-info' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('cmake' 'ninja' 'bison' 'flex' 'qt5-tools' 'python3')
url='https://www.wireshark.org/'
install=${pkgname}.install
source=(https://www.wireshark.org/download/src/${pkgname}-${pkgver}.tar.xz
        wireshark.sysusers)
sha256sums=('ebb1eebe39bcecee02195dc328dd25f6862fc9e9dea4c2e29eae50537d5eb4f2'
            'df07748152443c7d727bd51cd57af950c345b7141986b4f0e476cd6aa3623ac4')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's| Rev Unknown from unknown||' -i tools/make-version.pl
}

build() {
  cd ${pkgname}-${pkgver}
  cmake . -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_RPATH= \
    -DCMAKE_SKIP_RPATH=ON
  ninja
}

package() {
  cd ${pkgname}-${pkgver}

  DESTDIR="${pkgdir}" ninja install

  # wireshark uid group is 150
  install -Dm644 "${srcdir}/wireshark.sysusers" "${pkgdir}/usr/lib/sysusers.d/wireshark.conf"
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
}
