# Include global configuration

pkgname=kdepim-runtime
pkgver=19.08.2
pkgrel=3
pkgdesc='Extends the functionality of kdepim'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=(hicolor-icon-theme libkgapi libkolab akonadi akonadi-calendar
         knotifyconfig kross kalarmcal kmbox kimap qt5-speech syndication kdav pimcommon)
makedepends=(extra-cmake-modules kdoctools boost qca qt5-networkauth)
options=('debug')

source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('9b98980003d2d107596e9acc9482dfc3ea26a2485c75a700bd82b53b9be72ebf'
            'SKIP')

validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
