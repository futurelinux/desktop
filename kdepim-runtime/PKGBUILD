# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>
#               Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=19.08.3
pkgrel=1
pkgdesc="Extends the functionality of KDE PIM"
url="https://kontact.kde.org"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(libkolabxml kdav akonadi-calendar knotifyconfig kalarmcal kmbox pimcommon akonadi-notes akonadi qca qt5-networkauth)
makedepends=(extra-cmake-modules kdoctools boost)
options=(debug)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('2673967456b7eacbd69ca678761e479bfa65a0ec6455c86b3a83c76d766a71ea'
            'SKIP')

validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
