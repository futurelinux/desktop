pkgname=gpxsee
_pkgname=GPXSee
pkgver=7.17
pkgrel=1
pkgdesc='GPS log file viewer and analyzer'
arch=(x86_64)
url='https://www.gpxsee.org/'
license=(GPL3)
depends=(desktop-file-utils qt5-imageformats)
makedepends=(qt5-tools)
source=(https://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('d639c15ecf90583354bc4178efc5fd64e90f308f875acb64c0c77959e1b4fb89')

build() {
  cd $_pkgname-$pkgver
  lrelease-qt5 gpxsee.pro
  qmake-qt5 gpxsee.pro PREFIX=/usr
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  # Install appdata
  install -Dm644 pkg/appdata.xml "$pkgdir"/usr/share/metainfo/$pkgname.appdata.xml
}
