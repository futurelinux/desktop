pkgname=dosbox
pkgver=0.74.3
pkgrel=1
pkgdesc="An emulator with builtin DOS for running DOS Games"
arch=('x86_64')
url='https://www.dosbox.com/'
license=('GPL')
depends=('sdl_net' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'gcc-libs' 'zlib' 'glu')
makedepends=('mesa-libgl' 'gendesk')
categories=('games' 'system')
source=("https://downloads.sourceforge.net/${pkgname}/$pkgname-0.74-3.tar.gz"
	'dosbox.png')
md5sums=('759c75fffb59c542f80fb8391012911b'
         '2aac25fc06979e375953fcc36824dc5e')

prepare() {
  cd ${srcdir}
  gendesk --pkgname "${pkgname}" --pkgdesc "${pkgdesc}"
  
  cd ${srcdir}/${pkgname}-0.74-3
  sed -i 's/png_check_sig/png_sig_cmp/' configure 
}

build(){
  cd ${srcdir}/${pkgname}-0.74-3

  ./configure --prefix=/usr --sysconfdir=/etc/dosbox 
  make
}

package() {
  cd ${srcdir}/${pkgname}-0.74-3
  make DESTDIR=${pkgdir} install 

  # install docs, make does not install them
  install -Dm644 README "$pkgdir"/usr/share/doc/$pkgname/README
  install -Dm644 docs/README.video "$pkgdir"/usr/share/doc/$pkgname/README.video

  install -Dm644 ${srcdir}/${pkgname}.png \
    ${pkgdir}/usr/share/pixmaps/${pkgname}.png 
  install -Dm644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop 
}
