# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/libkomparediff2/
# Include global configuration

pkgname=libkomparediff2
pkgver=19.08.2
pkgrel=2
pkgdesc="Library to compare files and strings"
url='https://projects.kde.org/projects/kde/kdesdk/libkomparediff2'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kparts')
makedepends=('extra-cmake-modules' 'python3')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c5738f96dbda3d7272ad08ff9518722ae9b7ee737ab7e27c9e88cedb418371d7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
