pkgname=qbittorrent
pkgver=4.2.2
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt using the good libtorrent library"
arch=('x86_64')
url="https://www.qbittorrent.org/"
license=('GPL')
depends=('qt5-base' 'qt5-svg' 'hicolor-icon-theme' 'libtorrent-rasterbar' 'xdg-utils' 'boost-libs' 'python3' 'geoip-database')
makedepends=('boost' 'geoip' 'qt5-tools')
source=("https://downloads.sourceforge.net/sourceforge/qbittorrent/${pkgname}-${pkgver}.tar.xz")
sha256sums=('fa457cc6fef02fe15958f94863181e64b8dcd9762184f8a53ad4c75f89d42223')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    # tell qmake not to break makepkg's debug/!strip options
    export QBT_ADD_CONFIG='nostrip'

    ./configure --prefix=/usr
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make INSTALL_ROOT=${pkgdir} install
    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING
}
