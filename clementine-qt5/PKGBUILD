pkgname=clementine-qt5
_pkgname=Clementine
pkgver=1.4.0rc1
pkgrel=1
pkgdesc="Experimental Qt5 version of Clementine, a modern music player and library organiser."
arch=('x86_64')
license=('GPL')
depends=('gstreamer' 'taglib' 'glew' 'desktop-file-utils' 'hicolor-icon-theme'
         'qt5-base' 'libechonest' 'protobuf' 'crypto++' 'chromaprint'
         'libmygpo-qt5' 'qt5-x11extras' 'libgpod' 'liblastfm' 'sparsehash' 'libcdio' 'libmtp' 'fftw')
makedepends=('git' 'boost' 'cmake' 'mesa' 'qt5-tools' 'gst-libav')
optdepends=('gst-plugins-base: for more open formats'
            'gst-plugins-good: for use with "Good" plugin libraries'
            'gst-plugins-bad: for use with "Bad" plugin libraries'
            'gst-plugins-ugly: for use with "Ugly" plugin libraries'
            'libspotify: add support for Spotify, due to license restriction, please build it 
                         with ccr yourself'
            'dropbox: add support for Dropbox')
url="http://www.clementine-player.org/"
source=("https://github.com/clementine-player/$_pkgname/archive/$pkgver.tar.gz")
sha256sums=('ad493a1954594283d41a1575341118e19098463ba5626a37b893d8384e0a1f7b')
conflicts=('clementine')

prepare() {
  cd ${_pkgname}-$pkgver
  mkdir -p build
  sed 's,<lastfm5,<lastfm,g' -i src/internet/lastfm/* -i src/core/*.cpp
}

build() {
  cd ${_pkgname}-$pkgver/build
  cmake ".." -DLASTFM5_INCLUDE_DIRS=/usr/include/lastfm \
             -DLASTFM51_INCLUDE_DIRS=/usr/include/lastfm \
             -DCMAKE_INSTALL_PREFIX=/usr \
             -DBUILD_WERROR=OFF
  make 
}

package(){
  cd ${_pkgname}-$pkgver/build
  make DESTDIR=${pkgdir} install
}
