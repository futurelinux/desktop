# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=rocs
pkgver=19.08.3
pkgrel=1
pkgdesc="Rocs graph theory"
url="http://kde.org/applications/education/rocs/"
arch=('x86_64')
screenshot='https://www.kde.org/images/screenshots/rocs.png'
license=('GPL' 'LGPL' 'FDL')
depends=('grantlee' 'kdeclarative' 'ktexteditor' 'qt5-webkit')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'boost')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeedu')
replaces=('kdeedu-rocs')
conflicts=('kdeedu-rocs')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('533ab2f17160c402550479d7d3be9132b49e1d745c021c7735f0687d12236c8e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  export CXXFLAGS+=" -fext-numeric-literals"
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
