# Contributions from AUR: https://aur.archlinux.org/packages/proftpd/

pkgname=proftpd
pkgver=1.3.6
_pkgver=1.3.6
pkgrel=1
pkgdesc="A high-performance, scalable FTP server"
arch=('x86_64')
url="http://www.proftpd.org"
license=('GPL')
depends=('libldap' 'libmariadbclient' 'postgresql-libs')
backup=('etc/proftpd.conf' 'etc/conf.d/proftpd')
categories=('network')
install=proftpd.install
source=("ftp://ftp.proftpd.org/distrib/source/${pkgname}-${_pkgver}.tar.gz"
	'proftpd.service'
        'proftpd.logrotate'
	'proftpd.tmpfiles')
sha256sums=('91ef74b143495d5ff97c4d4770c6804072a8c8eb1ad1ecc8cc541b40e152ecaf'
            '64a239e0a65ba405a23135adf5f20fb415a54b8682caab587f20d12755074ac6'
            '52f94204558f52432d5bbdc6e0acefb30194bd602568f2adb0b92aac43ec860b'
            '359cb8f5b30e66627929f7c50cbdd7dcc6919f7261f36eb617045210caf90abb')

build() {
	cd ${srcdir}/${pkgname}-${_pkgver}

	./configure --prefix=/usr \
	    --mandir=/usr/share/man \
	    --libexecdir=/usr/lib \
	    --disable-pam \
	    --with-modules=mod_quotatab:mod_quotatab_file:mod_tls:mod_ldap:mod_sql:mod_sql_mysql:mod_sql_postgres \
	    --sysconfdir=/etc \
	    --localstatedir=/run/proftpd \
	    --enable-ctrls \
	    --enable-ipv6 \
	    --with-includes=/usr/include/mysql:/usr/include/postgresql \
	    --with-libraries=/usr/lib/mysql:/usr/lib/postgresql \
	    --enable-nls \
	    --with-systemd
	make
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  make DESTDIR="${pkgdir}" install
  sed -i 's|nogroup|nobody|g' "${pkgdir}/etc/proftpd.conf"

  install -Dm644 ../proftpd.logrotate "${pkgdir}/etc/logrotate.d/proftpd"
  install -Dm755 contrib/xferstats.holger-preiss "${pkgdir}/usr/bin/ftpstats"

  install -d "${pkgdir}/usr/lib/systemd/system/"
  install -m644 "${srcdir}"/proftpd.service "${pkgdir}/usr/lib/systemd/system/"
  install -Dm644 "${srcdir}"/proftpd.tmpfiles \
    "${pkgdir}"/usr/lib/tmpfiles.d/proftpd.conf

  # /run is tmpfs
  rmdir "${pkgdir}"/run/{proftpd,}
}
