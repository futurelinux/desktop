pkgname=mythtv
pkgver=31.0
pkgrel=1
pkgdesc="A Homebrew PVR project"
arch=('x86_64')
url="http://www.mythtv.org/"
license=('GPL')
depends=('avahi' 'fftw' 'lame' 'libass' 'libavc1394' 'libcdio' 'libiec61883'
         'libgl' 'libpulse' 'libva' 'libvpx' 'libxinerama' 'lirc' 'libxrandr'
         'mariadb-clients' 'mysql-python' 'perl-dbd-mysql' 'exiv2'
         'perl-io-socket-inet6' 'perl-libwww' 'perl-net-upnp' 'python3-lxml'
         'qt5-webkit' 'qt5-script' 'taglib' 'urlgrabber' 'libx264' 'libvdpau' 'libsamplerate' 'libxnvctrl')
makedepends=('glew' 'libcec' 'libxml2' 'mesa' 'openssl' 'x264' 'x265' 'yasm' 'gdb')
optdepends=('glew: for GPU commercial flagging'
            'libcec: for consumer electronics control capabilities')
source=("$pkgname-$pkgver.tar.gz::https://github.com/MythTV/$pkgname/archive/v$pkgver.tar.gz"
	'mythbackend.service'
        '99-mythbackend.rules'
        'sysusers.d')
sha256sums=('096ace898e4a075293bb720bd9f33ae5818650f0ef62e722db290c68e522612f'
            '086f573884e4df1ff6e6e8de5a423a9ff900166acfe085abe65b62f32730e4a3'
            'ecfd02bbbef5de9773f4de2c52e9b2b382ce8137735f249d7900270d304fd333'
            '470de0a4050c16c7af11a0e5cfe2810b7daae42df4acf5456c7eae274dc7c5ae')

prepare() {
  cd $srcdir/$pkgname-$pkgver/$pkgname
  
  find 'bindings/python' 'contrib' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python3@'

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../../$src"
  done
}

build() {
  cd $srcdir/$pkgname-$pkgver/$pkgname

  ./configure --prefix=/usr \
              --cpu=x86-64 \
              --disable-altivec \
              --disable-audio-jack \
              --disable-ccache \
              --disable-distcc \
              --enable-pic \
              --enable-libmp3lame \
              --enable-libvpx \
              --enable-libx264 \
              --enable-libx265 \
              --python=python3 \
              --perl-config-opts=INSTALLDIRS=vendor
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/$pkgname
  make INSTALL_ROOT=$pkgdir install

  install -D -m644 "$srcdir/mythbackend.service" "$pkgdir/usr/lib/systemd/system/mythbackend.service"
  install -D -m644 'database/mc.sql' "$pkgdir/usr/share/mythtv/mc.sql"
  install -D -m644 "$srcdir/sysusers.d" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  cp -R 'contrib' "$pkgdir/usr/share/mythtv"
  mkdir -p "$pkgdir/var/log/mythtv"
  
  # Install udev rules
  # https://www.mythtv.org/wiki/Systemd_mythbackend_Configuration#Delay_starting_the_backend_until_tuners_have_initialized
  install -Dm644 "$srcdir"/99-mythbackend.rules "$pkgdir"/usr/lib/udev/rules.d/99-mythbackend.rules
}
