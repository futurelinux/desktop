pkgbase=python-asn1crypto
pkgname=('python3-asn1crypto' 'python2-asn1crypto')
pkgver=0.24.0
pkgrel=1
arch=('any')
license=('MIT')
pkgdesc="Python ASN.1 library with a focus on performance and a pythonic API"
url="https://github.com/wbond/asn1crypto"
makedepends=('python3-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/wbond/asn1crypto/archive/$pkgver.tar.gz")
sha256sums=('d2a82bb3c29e1abcb376b59679510b58dd796c813f8983f2db9b45e9ab228190')

prepare() {
  cp -a asn1crypto-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/asn1crypto-$pkgver
  python3 setup.py build

  cd "$srcdir"/asn1crypto-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/asn1crypto-$pkgver
  python3 run.py tests

  cd "$srcdir"/asn1crypto-$pkgver-py2
  python2 run.py tests
}

package_python3-asn1crypto() {
  depends=('python3')

  cd asn1crypto-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/python-asn1crypto/LICENSE
}

package_python2-asn1crypto() {
  depends=('python2')

  cd asn1crypto-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/python2-asn1crypto/LICENSE
}
