pkgname=streamlink
pkgver=1.3.1
pkgrel=1
pkgdesc='CLI program that launches streams from various streaming services in a custom video player (livestreamer fork)'
arch=('x86_64')
url='https://streamlink.github.io/'
license=('BSD')
depends=("python3-"{pycryptodome,isodate,pycountry,requests,pysocks,websocket-client} 'rtmpdump')
makedepends=("python3-"{recommonmark,setuptools,sphinx})
optdepends=('ffmpeg: Required to play streams that are made up of separate audio and video streams, eg. YouTube 1080p+')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
validpgpkeys=('E2B794C7C2C37162E5E2A097E3DB9E282E390FA0') # Streamlink Signing Key <streamlink@protonmail.com>
sha256sums=('dee047f688dc31429842ad29870ab19e63b292d73e79d709adcdbca1f278d432'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  export STREAMLINK_USE_PYCOUNTRY="true"
  python3 setup.py build
  python3 setup.py build_sphinx -b man
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 build/sphinx/man/streamlink.1 \
    "$pkgdir/usr/share/man/man1/streamlink.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
