# Contributions from AUR: https://aur.archlinux.org/packages/merkaartor/

pkgname=merkaartor
pkgver=0.18.4
pkgrel=1
pkgdesc="An editor for the OpenStreetMap project"
arch=('x86_64')
url="https://github.com/openstreetmap/merkaartor"
license=('GPL2')
depends=('desktop-file-utils' 'exiv2' 'gdal' 'libxml2' 'libxslt' 'proj'
	 'qt5-webkit' 'qt5-svg' 'qt5-tools' 'libspatialite' 'unixodbc')
optdepends=('gpsd')
categories=('network' 'education')
screenshot=(http://merkaartor.be/attachments/download/195)
source=("https://github.com/openstreetmap/merkaartor/archive/${pkgver}.tar.gz")
sha512sums=('16001688584fd13eb7573159092a21ed0189ae0ce900474bacd5b1e47b8cebb0c4b2bd5b904e13a0029ec01167b68a32a9936914b3f5a1360b389aa31858cf9c')

prepare() {
  cd $srcdir/$pkgname-$pkgver/src
  sed -i s!'Education;Geography;Network;Qt;'!'Education;Geography;'! \
  $pkgname.desktop
}

build() {
  cd $srcdir/$pkgname-$pkgver
  echo 'QMAKE_MOC = $$QMAKE_MOC -DBOOST_TT_HAS_OPERATOR_HPP_INCLUDED' >> src/src.pro
  lrelease-qt5 src/src.pro
  qmake-qt5 \
    NODEBUG=1 \
    PREFIX="/usr" \
    RELEASE=1 \
    Merkaartor.pro
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make INSTALL_ROOT=$pkgdir install
}
