pkgname=suil
pkgver=0.10.6
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=('x86_64')
url="https://drobilla.net/software/suil/"
license=('custom:ISC')
depends=('lv2' 'qt5-base')
makedepends=('python3')
source=("https://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
sha256sums=('06fc70abaa33bd7089dd1051af46f89d378e8465d170347a3190132e6f009b7c')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove local call to ldconfig
  sed -i "/ldconfig/d" wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  python3 waf configure --prefix=/usr
  python3 waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  python3 waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "$pkgdir/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,NEWS,PACKAGING,README.md}
}
