# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kdevelop

pkgname=kdevelop
pkgver=5.4.5
pkgrel=1
pkgdesc='A C/C++ development environment for KDE'
arch=(x86_64)
url='http://www.kdevelop.org/'
license=(GPL)
options=('debug')
depends=(clang libksysguard kitemmodels knotifyconfig ktexteditor grantlee libkomparediff2 qt5-webengine
         kcmutils threadweaver knewstuff)
makedepends=(extra-cmake-modules kdoctools kdevelop-pg-qt llvm qt5-tools plasma-framework krunner okteta python3 mesa
             boost purpose subversion)
optdepends=('konsole: embedded terminal'
	'git: Git support'
	'subversion: SVN support'
	'cvs: CVS support'
	'gdb: GNU Debugger support'
	'qt5-doc: Qt documentation integration'
	'cmake: cmake integration'
	'qt5-tools: qthelp plugin'
 	'okteta: hex editor integration'
	'plasma-framework: for the plasma addons'
	'cppcheck: code analyzer'
	'heaptrack: memory profiler'
	'kdev-clang-tidy: clang-tidy analyzer plugin')
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('92c0504ff8c3ef730881e021adc7d6e945d8db0cbc0f3ff81280008ad35b7023'
            'SKIP')
validpgpkeys=('0A48BC961075B4BA8523E3790A345FB086E797D9')

prepare() {
  mkdir -p build
  sed -i 's|qdbus|qdbus-qt5|g' "${pkgname}-${pkgver}/app/kdevelop!"
}

build() {
  cd build
  cmake  ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make 
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
