pkgname=ktorrent
pkgver=5.2.0
pkgrel=2
pkgdesc="A powerful BitTorrent client for KDE"
arch=('x86_64')
url='https://kde.org/applications/en/internet/org.kde.ktorrent'
license=('GPL2')
depends=('libktorrent' 'kcmutils' 'knotifyconfig' 'kross' 'phonon-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'plasma-workspace' 'kdnssd'
             'taglib' 'syndication' 'kplotting' 'kdewebkit' 'geoip')
optdepends=('plasma-workspace: shutdown plugin'
            'kdnssd: zeroconf plugin'
            'taglib: mediaplayer plugin'
            'geoip: info widget plugin'
            'syndication: syndication plugin'
            'kdewebkit: search plugin, syndication plugin'
            'kplotting: statistics plugin')
options=('debug')
source=("https://download.kde.org/stable/ktorrent/$pkgver/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('d1031d745c1ace02d11b59c426610aee779d208e3d1ae81f8559c265173732ff'
            'SKIP')
validpgpkeys=(1EE5A3205904BAA2B88C0A9D24FD31940095C0E1) # Andrius Å tikonas <andrius@stikonas.eu>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DWITH_SYSTEM_GEOIP=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
