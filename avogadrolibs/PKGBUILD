# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/avogadrolibs/

pkgname=avogadrolibs
pkgver=1.93.0
pkgrel=2
pkgdesc="An advanced molecular editor"
arch=(x86_64)
url="https://www.openchemistry.org/"
license=(custom)
depends=(molequeue glew spglib libmsym libarchive hdf5)
makedepends=(cmake eigen3 python3 boost qt5-tools vtk)
optdepends=('vtk: For the Qt plugins')
source=($pkgname-$pkgver.tar.gz::"https://github.com/OpenChemistry/avogadrolibs/archive/$pkgver.tar.gz")
md5sums=('d27c6f1a4653577a063260969d3e72ce')


prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DUSE_VTK=ON \
    -DUSE_HDF5=ON \
    -DUSE_MMTF=OFF \
    -DUSE_PYTHON=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

  # Move python module to the correct place
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  mkdir -p "$pkgdir${site_packages}"
  mv "$pkgdir"/usr/lib/avogadro "$pkgdir${site_packages}"
}
