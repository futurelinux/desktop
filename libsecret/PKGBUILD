
pkgname=libsecret
pkgver=0.18.8
pkgrel=1
pkgdesc="Library for storing and retrieving passwords and other secrets"
arch=(x86_64)
license=(LGPL)
url="https://wiki.gnome.org/Projects/Libsecret"
depends=(glib2)
makedepends=(intltool docbook-xsl gobject-introspection vala git gtk-doc)
_commit=b5442654d483e959ac9ecd3a3fb9eebc8d9d8399  # tags/0.18.8^0
source=("git+https://git.gnome.org/browse/libsecret#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --disable-gcrypt --enable-gtk-doc=no
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
} 
