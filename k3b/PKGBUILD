# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>
#               Eric BÃ©langer <eric@archlinux.org>

pkgname=k3b
pkgver=19.08.3
pkgrel=1
pkgdesc="Feature-rich and easy to handle CD burning application"
url="https://userbase.kde.org/K3b"
arch=(x86_64)
license=(GPL)
depends=(libkcddb kcmutils knotifyconfig libmad kfilemetadata knewstuff
         libmpcdec libdvdread libburn qt5-webkit)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('cdrtools: for CD burning with cdrecord'
            'dvd+rw-tools: for DVD burning support'
            'vcdimager: for VCD burning support'
            'emovix: for bootable multimedia CD/DVD support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for CD ripping support'
            'transcode: for DVD ripping support')
options=(debug)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('197ea67fa9ef66f8ee084608fd1907e2b3db108eec643d5bd6a0980f838f2b23'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>
          
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DK3B_ENABLE_PERMISSION_HELPER=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
