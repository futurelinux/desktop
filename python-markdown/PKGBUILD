# Contributions from Arch:
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrew Antle <andrew dot antle at gmail dot com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chaiwat Suttipongsakul <cwt at bashell dot com>

pkgbase=python-markdown
pkgname=('python3-markdown' 'python2-markdown')
_pkgbasename=Markdown
pkgver=3.1
pkgrel=1
pkgdesc="Python implementation of John Gruber's Markdown."
arch=('any')
url='https://pypi.org/project/Markdown/'
license=('BSD')
makedepends=('python3' 'python2' 'python3-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/M/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz")
md5sums=('ef00dbf71b44d60a8e75b0315eaf6944')

build() {
  # bug in 2.4, some DOS line endings slipped in
  find "$_pkgbasename-$pkgver/" -name '*py' | xargs sed -i 's|\r||g'

  cp -r $_pkgbasename-$pkgver "$srcdir/python2-markdown"
  cd "$srcdir/python2-markdown"
  find "$srcdir/python2-markdown" -name '*py' | xargs sed -i 's|#!/usr/bin/env python$|&2|'
}

package_python3-markdown() {
  depends=('python3')
  cd "$srcdir/$_pkgbasename-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=0
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/python-markdown/LICENSE"
}

package_python2-markdown() {
  depends=('python2')
  cd "$srcdir/python2-markdown"
  python2 setup.py install --root="$pkgdir" --optimize=0
  mv "$pkgdir/usr/bin/markdown_py" "$pkgdir/usr/bin/markdown_py2"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/python2-markdown/LICENSE"
}
