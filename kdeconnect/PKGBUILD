pkgname=kdeconnect
pkgver=1.4
pkgrel=1
epoch=1
pkgdesc='Adds communication between KDE and your smartphone'
arch=(x86_64)
url='https://community.kde.org/KDEConnect'
license=(GPL)
depends=(hicolor-icon-theme kcmutils kwayland libfakekey qca kpeoplevcard pulseaudio-qt kirigami)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('sshfs: remote filesystem browser')
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-kde-$pkgver.tar.xz")
sha256sums=('caee7945a9d9bb881a943dc8d2fd0d702c04da5bdb2df14d4f875e7cf5d5261a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-kde-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
