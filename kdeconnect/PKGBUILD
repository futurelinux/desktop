source ../kdeapps.conf

pkgname=kdeconnect
pkgver=1.3.5
pkgrel=1
epoch=1
pkgdesc='Adds communication between KDE and your smartphone'
arch=(x86_64)
url='https://community.kde.org/KDEConnect'
license=(GPL)
depends=(hicolor-icon-theme kcmutils kwayland libfakekey qca-qt5)
makedepends=(extra-cmake-modules kdoctools python3 qt5-declarative)
optdepends=('sshfs: remote filesystem browser'
            'kde-cli-tools: configuration UI'
            'qt5-declarative: QML bindings')
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-kde-$pkgver.tar.xz")
sha256sums=('e5ce7084427a831e6506494b656af9c1dd7bf29b7458c2546baf0a5d7a1f990a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-kde-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
