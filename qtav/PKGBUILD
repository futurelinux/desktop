# Contributions from Arch
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qtav
pkgver=1.13.0
pkgrel=1
pkgdesc="A cross-platform multimedia playback framework based on Qt and FFmpeg"
arch=('x86_64')
url="http://www.qtav.org/"
license=('LGPL')
depends=('qt5-declarative' 'qt5-svg' 'qt5-quickcontrols' 'ffmpeg' 'libxv' 'openal' 'uchardet' 'desktop-file-utils')
source=(https://github.com/wang-bin/QtAV/archive/v${pkgver}.tar.gz
        include-qsgmaterial.patch::"https://github.com/wang-bin/QtAV/commit/5abba7f0505e75fceabd4dd8992a7e02bb149d64.patch")
sha256sums=('a493d54d105b32c526f05188e4d7a8cf3508ecac697aae088721ffad20982288'
            'cf6e80e5102356e4a72706f1f7a13396a3911954afe900a957112c56f5c280d5')

prepare() {
  cd QtAV-$pkgver
# Fix build with qt 5.14
  patch -p1 -i ../include-qsgmaterial.patch
}

build() {
  cd QtAV-${pkgver}

  qmake-qt5 "CONFIG+=no_rpath recheck"
  make
}

package() {
  cd QtAV-${pkgver}

  make INSTALL_ROOT="${pkgdir}" install
}
