pkgname=avogadro
_pkgname=avogadroapp
pkgver=1.91.0
pkgrel=1
pkgdesc="An advanced molecular editor based on Qt"
arch=('x86_64')
url="http://openchemistry.org/projects/avogadro2"
license=('custom:Kitware')
depends=('avogadrolibs' 'desktop-file-utils')
makedepends=('git' 'cmake' 'eigen3')
categories=('education')
install=$pkgname.install
source=(https://github.com/OpenChemistry/avogadroapp/archive/$pkgver.tar.gz)
md5sums=('0f0b9d0f7220f3ffee00f837ea49c308')

prepare() {
  mkdir -p $_pkgname-$pkgver/build
}

build() {
  cd $_pkgname-$pkgver/build

  cmake .. \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_SHARED_LIBS=ON \
      -DENABLE_TESTING=OFF \
      -DUSE_VTK=OFF
  make
}

package() {
  cd $_pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
  install -D -m 644 ../LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
