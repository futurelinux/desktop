# Maintainer: UtG  <utg[dot]chakra.linux[at]gmail[dot]com>

pkgbase=python-requests
pkgname=(python2-requests python3-requests)
pkgver=2.21.0
pkgrel=1
_libname=requests
pkgdesc="Requests is an Apache2 Licensed HTTP library, written in Python, for human beings."
url="http://python-requests.org/"
makedepends=('python2-setuptools' 'python3-setuptools' 'python3-chardet' 'python2-chardet'
             'python3-urllib3' 'python2-urllib3' 'python3-idna' 'python2-idna')
checkdepends=('python3-pytest-httpbin' 'python2-pytest-httpbin'  'python3-pysocks' 'python2-pysocks' 'python3-pytest-mock' 'python2-pytest-mock')
license=('Apache')
arch=('any')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/kennethreitz/requests/archive/v$pkgver.tar.gz"
        certs.patch)
sha512sums=('934c329e6631ec6089577c49651b73265f0c3f0829b9151e1463dea905f35820a03ec3b0ee6a2ab2292d213b715f0b2348110392d60f55ea1cbe4b24fca4f890'
            '424a3bb01b23409284f6c9cd2bc22d92df31b85cfd96e1d1b16b5d68adeca670dfed4fff7977d8b10980102b0f780eacc465431021fcd661f3a17168a02a39a3')

prepare() {
    cd "$srcdir"/requests-$pkgver
    sed -e '/certifi/d' \
        -e "s/,<.*'/'/" \
        -i setup.py
    patch -p1 -i "$srcdir"/certs.patch
        
    cd "$srcdir"
    cp -a requests-$pkgver{,-py2}
    find requests-$pkgver -name \*.py -exec sed -r 's|^#!(.*)python$|#!\1python3|' -i {} +
    find requests-$pkgver-py2 -name \*.py -exec sed -r 's|^#!(.*)python$|#!\1python2|' -i {} +
}

build() {
    cd "$srcdir"/requests-$pkgver
    python3 setup.py build

    cd "$srcdir"/requests-$pkgver-py2
    python2 setup.py build
}

check() {
    cd "$srcdir"/requests-$pkgver
    py.test tests -k "not test_DIGEST_HTTP_200_OK_GET"

    cd "$srcdir"/requests-$pkgver-py2
    py.test2 tests -k "not test_DIGEST_HTTP_200_OK_GET"
}

package_python3-requests() {
    depends=('python3-urllib3' 'python3-chardet' 'python3-idna')
    optdepends=('python3-pysocks: SOCKS proxy support')

    cd "$srcdir"/requests-$pkgver
    python3 setup.py install --skip-build -O1 --root="$pkgdir"
    install -m0644 -D "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-requests() {
    depends=('python2-urllib3' 'python2-chardet' 'python2-idna')
    optdepends=('python2-ndg-httpsclient: HTTPS requests with SNI support'
                'python2-grequests: asynchronous requests with gevent'
                'python2-pysocks: SOCKS proxy support')

    cd "$srcdir"/requests-$pkgver-py2
    python2 setup.py install --skip-build -O1 --root="$pkgdir"
}
