# Contributions from Arch:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred.com>
# Contributor: Bogdan Szczurek <thebodzio@gmail.com>
# Contributor: Ismo Toijala <ismo.toijala@gmail.com>

pkgbase=python-jsonschema
pkgname=('python3-jsonschema' 'python2-jsonschema')
pkgver=3.0.2
pkgrel=1
pkgdesc="An implementation of JSON Schema validation for Python"
arch=('any')
url="https://pypi.python.org/pypi/jsonschema"
license=('MIT')
makedepends=('python3-attrs' 'python2-attrs' 'python3-pyrsistent' 'python2-pyrsistent'
             'python2-functools32' 'python3-setuptools-scm' 'python2-setuptools-scm')
source=("$pkgbase-$pkgver.tar.bz2::https://github.com/Julian/jsonschema/archive/v$pkgver.tar.gz")
sha256sums=('0517eca54d3abdf3c0ae6d4355dc6205c337b314dc97b5e4cd172450cf77e6fe')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cp -a jsonschema-$pkgver{,-py2}
  find jsonschema-$pkgver-py2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build() {
  cd "$srcdir"/jsonschema-$pkgver
  python setup.py build

  cd "$srcdir"/jsonschema-$pkgver-py2
  python2 setup.py build
}

package_python3-jsonschema() {
  depends=('python3-attrs' 'python3-pyrsistent' 'python3-setuptools')

  cd jsonschema-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 json/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jsonschema() {
  depends=('python2-attrs' 'python2-pyrsistent' 'python2-setuptools' 'python2-functools32')

  cd jsonschema-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 json/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/jsonschema{,2}
}
