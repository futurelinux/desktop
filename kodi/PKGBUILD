pkgname=kodi
pkgver=18.6
_codename=Leia
pkgrel=2
arch=('x86_64')
url="https://kodi.tv"
license=('GPL2')
pkgdesc="A software media player and entertainment hub for digital media"
  # depends expected for kodi plugins:
  # 'python2-pillow' 'python2-pybluez' 'python2-simplejson'
  # depends expected in FEH.py
  # 'mesa-demos' 'xorg-utils'
depends=('python2-pillow' 'python2-simplejson' 'mesa-demos' 'bluez-libs' 'fribidi' 'glew' 'hicolor-icon-theme'
         'libcdio' 'libjpeg-turbo' 'libmariadbclient' 'libmicrohttpd' 'libpulse' 'libssh' 'libva' 'libxrandr'
         'libxslt' 'lirc' 'lzo' 'smbclient' 'taglib' 'tinyxml' 'yajl' 'libusb' 'nettle' 'dcadec' 'libbluray'
         'curl' 'libcec' 'lsb-release' 'libnfs' 'xorg-utils')
optdepends=(
    'bluez: Blutooth support'
    'libnfs: NFS shares support'
    'libplist: AirPlay support'
    'shairplay: AirPlay support'
    'pulseaudio: PulseAudio support'
    'udisks: Automount external drives'
    'unrar: Archives support'
    'unzip: Archives support'
    'upower: Display battery level')
makedepends=(
  'autoconf' 'automake' 'libusb' 'cmake' 'groff'
  'bluez-libs' 'boost' 'cmake' 'curl' 'doxygen' 'git' 'glew'
  'gperf' 'hicolor-icon-theme' 'jasper' 'java-runtime' 'libass' 'libaacs'
  'libbluray' 'libcdio' 'libcec' 'libgl' 'libmariadbclient' 'libmicrohttpd'
  'libmodplug' 'libmpeg2' 'libnfs' 'libplist' 'libpulse' 'libssh' 'libva'
  'libvdpau' 'libxrandr' 'libxslt' 'lzo' 'nasm' 'nss-mdns' 'python2-pillow'
  'python2-simplejson' 'rtmpdump' 'smbclient' 'swig' 'taglib' 'tinyxml'
  'unzip' 'upower' 'yajl' 'zip' 'rapidjson' 'ghostscript' 'shairplay' 'ccache'
)
provides=('xbmc')
conflicts=('xbmc')
replaces=('xbmc')
source=("$pkgname-$pkgver-$_codename.tar.gz::https://github.com/xbmc/xbmc/archive/$pkgver-$_codename.tar.gz")
sha256sums=('47e6d7d4e01dbda92ff83a3e141ac43003e918133e78b3a4b79faff65184711c')

prepare() {
  mkdir kodi-build

  cd "xbmc-$pkgver-$_codename"
}

build() {
  cd kodi-build

  export MAKEFLAGS="-j1"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DENABLE_EVENTCLIENTS=ON \
        -DENABLE_INTERNAL_FFMPEG=ON \
        -DENABLE_INTERNAL_FMT=ON \
        -DENABLE_INTERNAL_CROSSGUID=ON \
        -DENABLE_INTERNAL_FSTRCMP=ON \
        -DENABLE_INTERNAL_FLATBUFFERS=ON \
        ../"xbmc-$pkgver-$_codename"
  make
  make preinstall
}

package() {
  cd kodi-build

  make DESTDIR="$pkgdir" install

  # python2 is being used
  cd "$pkgdir"
  grep -lR '#!.*python' * | while read file; do sed -s 's/\(#!.*python\)/\12/g' -i "$file"; done
}
