pkgname=libopenshot-audio
pkgver=0.2.0
pkgrel=2
pkgdesc="A high-quality audio editing and playback library used by libopenshot."
arch=('x86_64')
url="https://www.openshot.org/"
license=('GPL3')
depends=('alsa-lib' 'freetype2' 'libxcursor' 'libxinerama' 'libxrandr')
makedepends=('cmake' 'doxygen')
source=("https://github.com/OpenShot/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('937ff4f1c2dfb8ab5d56ad85beacaa29dfd5a79af0d9cf647386034fe9882309')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	mkdir build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib ..
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}/build"
	make DESTDIR="${pkgdir}" install
}
