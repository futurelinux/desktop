pkgname=kdev-php
pkgver=5.5.2
pkgrel=1
pkgdesc="PHP language and documentation plugin for KDevelop"
arch=(x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(kdevelop)
makedepends=(extra-cmake-modules kdoctools kdevelop-pg-qt)
source=("https://download.kde.org/stable/kdevelop/$pkgver/src/${pkgname}-$pkgver.tar.xz"{,.sig})
sha256sums=('05b8a2f1bdc86ce03f4c5121c29dadcf32daf52b34bbf54fbcd250494f3bd6a2'
            'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6'  # Sven Brauch <mail@svenbrauch.de>
              '364EFA5F639572907D392999C64CF56B13CACE5D'  # Kevin Funk <kfunk@kde.org>
              'E191FD5BE6F46870F09E82B2024E7FB43D015474'  # Friedrich W. H. Kossebau <kossebau@kde.org>
              '0A48BC961075B4BA8523E3790A345FB086E797D9') # https://share.kde.org/s/8dKaJTw8pR8iiwJ

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake  ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
