pkgname=kup
pkgver=0.8.0
pkgrel=1
pkgdesc="A KDE-based frontend for the bup backup software"
arch=('x86_64')
url="https://invent.kde.org/system/kup"
license=('GPL2')
depends=('kidletime' 'kinit' 'libgit2' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'plasma-framework')
optdepends=('bup: support for incremental backups'
            'rsync: support for synced backups')
source=("https://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('79154cd12bcbef87b3b22851adcd505f5da7723483361c7a16ae4c3eea61f0bd')
 
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}
 
package() {
  DESTDIR="$pkgdir" cmake --install build
}
