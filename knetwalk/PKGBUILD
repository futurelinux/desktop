# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=knetwalk
pkgver=19.08.2
pkgrel=3
pkgdesc="Connect all the terminals to the server, in as few turns as possible"
arch=('x86_64')
url='http://kde.org/applications/games/knetwalk/'
screenshot='https://www.kde.org/images/screenshots/knetwalk.png'
license=('GPL' 'LGPL' 'FDL')
depends=('knotifyconfig' 'libkdegames')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegames')
replaces=('kdegames-knetwalk')
conflicts=('kdegames-knetwalk')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('057b8571f165a716a6cc13ec6489e7b5f9b4c14fa72080180a2098fa0fa028a1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
