pkgname=octave
pkgver=4.4.1
pkgrel=7
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('readline' 'glu' 'fftw' 'curl' 'graphicsmagick' 'glpk' 'hdf5' 'qhull'
         'suitesparse' 'arpack' 'pcre' 'qt5-base' 'fontconfig' 'zlib' 'portaudio' 'qrupdate' 'freetype2' 'gl2ps'
         'qscintilla-qt5' 'libsndfile')
makedepends=('texlive-core' 'gcc-fortran' 'texinfo' 'gnuplot' 'openjdk' 'fltk')
optdepends=('texinfo: for help-support in octave'
	    'gnuplot: alternative plotting'
	    'openjdk: to call Java functions'
            'fltk: FLTK GUI')
source=(ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.gz)
options=('!emptydirs')
sha1sums=('693fe46fdf49c62dec9f81100050ba2d2c1e4067')

source+=(readonlydoc.patch::https://savannah.gnu.org/file/readonlydoc.patch?file_id=46245)
sha1sums+=('172fda9eace4b13d8dc7e5ee874158c9490664f5')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i $srcdir/readonlydoc.patch
}

build() {
  cd $pkgname-$pkgver
    
  ./configure --prefix=/usr \
              --libexecdir=/usr/lib \
              --enable-shared \
              --disable-static \
              --with-quantum-depth=16 \
              --with-umfpack="-lumfpack -lsuitesparseconfig" 
  MOC=moc UIC=uic
  
  LANG=C make
}

package(){
  cd $pkgname-$pkgver

  make DESTDIR=$pkgdir install

  # add path to ld.so.conf.d
  install -d $pkgdir/etc/ld.so.conf.d
  echo "/usr/lib/$pkgname/$pkgver" > "$pkgdir/etc/ld.so.conf.d/$pkgname.conf"
}
