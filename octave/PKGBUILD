pkgname=octave
pkgver=5.2.0
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('x86_64')
url="https://www.gnu.org/software/octave/"
license=('GPL')
depends=('readline' 'glu' 'fftw' 'curl' 'graphicsmagick' 'glpk' 'hdf5' 'qhull'
         'sundials' 'arpack' 'pcre' 'qt5-base' 'fontconfig' 'zlib' 'portaudio' 'qrupdate' 'freetype2' 'gl2ps'
         'qscintilla-qt5' 'libsndfile')
makedepends=('texlive-core' 'gcc-fortran' 'texinfo' 'gnuplot' 'openjdk' 'fltk')
optdepends=('texinfo: for help-support in octave'
	    'gnuplot: alternative plotting'
	    'openjdk: to call Java functions'
            'fltk: FLTK GUI')
source=(ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.gz
        octave-sundials4.patch)
options=('!emptydirs')
sha1sums=('bc9e4ceed93c7f7dcc692daeb9dccb182b2d5e9e'
          '221ceb1a7b449ee4a3579b49af705e05c2e66b3d')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../octave-sundials4.patch # Fix sundials support https://savannah.gnu.org/bugs/?52475
  autoreconf -vif
}

build() {
  cd $pkgname-$pkgver
    
  ./configure --prefix=/usr \
              --libexecdir=/usr/lib \
              --enable-shared \
              --disable-static \
              --with-quantum-depth=16 \
              --with-sundials_ida="-lsundials_ida -lsundials_sunlinsolklu"
  
  make
}

package(){
  cd $pkgname-$pkgver

  make DESTDIR=$pkgdir install

  # add path to ld.so.conf.d
  install -d $pkgdir/etc/ld.so.conf.d
  echo "/usr/lib/$pkgname/$pkgver" > "$pkgdir/etc/ld.so.conf.d/$pkgname.conf"
}
