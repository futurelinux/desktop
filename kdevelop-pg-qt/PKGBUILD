pkgname=kdevelop-pg-qt
pkgver=2.2.0
pkgrel=1
pkgdesc="KDevelop Parser Generator, a LL(1) parser generator used by KDevelop language plugins"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=(GPL)
option=('debug')
depends=(qt5-base)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha1sums=('1889d91b54a47603790cea41fa765b132dcac5f7'
          'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6'  # Sven Brauch <mail@svenbrauch.de>
              '364EFA5F639572907D392999C64CF56B13CACE5D') # Kevin Funk <kfunk@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
