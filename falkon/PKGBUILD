pkgname='falkon'
pkgver=3.1.0
pkgrel=4
pkgdesc="Cross-platform QtWebEngine browser"
url="https://github.com/KDE/falkon"
arch=('x86_64')
license=('GPL3')
depends=('qt5-webengine' 'qt5-x11extras' 'qt5-svg')
makedepends=('qt5-tools' 'kio' 'purpose' 'extra-cmake-modules' 'lcms2' 'pyside2' 'shiboken2' 'llvm' 'clang')
optdepends=('bash-completion: bash completion support'
            'kio: KDE integration'
            'purpose: KDE integration'
            'pyside2: Python plugins')
conflicts=('qupzilla')
replaces=('qupzilla')
categories=('network')
source=("https://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        'start-white.png'
        0001-branded-speeddial.patch
        falkon-pyside-5.12.2.patch::"https://cgit.kde.org/falkon.git/patch/?id=479933e0")
md5sums=('9255fb335d3ba361dea44b7b297ddf7d'
         'fc705d7aba952361a626a2c79726bbfa'
         'b1848d49f320719b944d00f28ca153a9'
         'cc88bcb9965c9bfa2bff7ac72837947f')

prepare() {
  msg 'Applying browserUI patch...'
  cd $pkgname-$pkgver/src/lib/app/
  sed -e 's|"falkon:start"|"https://www.chakralinux.org/?welcome"|' \
      -e 's|"showStatusBar", true|"showStatusBar", false|' \
      -e 's|"showMenubar", true|"showMenubar", false|g' -i browserwindow.cpp
  cd $srcdir/$pkgname-$pkgver/src/lib/plugins/
  sed -e 's|"background", QString()|"file:///usr/share/pixmaps/start-white.png", QString()|' \
      -e 's|"backsize", "auto"|"backsize", "contain"|' -i speeddial.cpp
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i ../0001-branded-speeddial.patch
  
  msg 'Applying preference patch'
  cd src/lib/preferences/
  sed -e 's|"homepage", QUrl(QSL("falkon:start"))|"homepage", QUrl(QSL("https://www.chakralinux.org/?welcome"))|' \
      -e 's|"showStatusBar", true|"showStatusBar", false|g' -i preferences.cpp
  msg 'Applying search engine patch...'
  cd ../opensearch/
  sed 's,\?q=\%s\&t=qupzilla,\?q=\%s\&t=chakra,g' -i searchenginesmanager.cpp

  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i ../falkon-pyside-5.12.2.patch # Fix build with PySide 5.12.2
}

build() {
  export USE_WEBGL="true"
  export ENABLE_OPACITY_EFFECT="true"

  cd $pkgname-$pkgver
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=None # https://bugreports.qt.io/browse/PYSIDE-980
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm 644 ../start-white.png $pkgdir/usr/share/pixmaps/start-white.png
  
  # zsh completion
  install -Dm644 linux/completion/_$pkgname \
    $pkgdir/usr/share/zsh/site-functions/_$pkgname
}
