# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=kwave
pkgver=19.08.2
pkgrel=3
pkgdesc="A sound editor for KDE"
arch=(x86_64)
url="http://kwave.sourceforge.net"
license=(GPL)
depends=(qt5-multimedia kio audiofile libmad libsamplerate hicolor-icon-theme id3lib fftw opus)
makedepends=(extra-cmake-modules kdoctools doxygen python librsvg)
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdemultimedia')
screenshot=('http://kde-apps.org/CONTENT/content-pre1/11781-1.png')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('7836b7737d3f578f6d44b40dddf8b3abae53eaa84b3352bfed242eb6ef3d5604'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"  \
    -DWITH_PULSEAUDIO=OFF \
    -DWITH_PHONON=OFF \
    -DWITH_MP3=ON
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
