pkgname=kphotoalbum
pkgver=5.6.1
pkgrel=1
pkgdesc="KDE Photo Album, picture collection manager"
url="https://www.kphotoalbum.org/"
arch=('x86_64')
license=('GPL')
depends=('libkipi' 'libkdcraw' 'libkgeomap' 'exiv2')
makedepends=('extra-cmake-modules' 'python3' 'plasma-framework' 'phonon-qt5' 'kdoctools' 'kparts' 'libkexiv2' 'purpose')
source=("https://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('3214a45d32156c06b6ff37ea1ddf7a443e635aa285c283a02326154f2c468304'
            'SKIP')
validpgpkeys=('8A53F0CB9F752E6F4F72FEC6F8DECD0DDF0F74A8') # Tobias Leupold <tobias.leupold@gmx.de>

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
