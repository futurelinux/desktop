pkgname=okteta
pkgver=0.26.3
pkgrel=1
epoch=1
arch=('x86_64')
pkgdesc="KDE hex editor for viewing and editing the raw data of files"
url="https://kde.org/applications/utilities/okteta/"
screenshot="https://www.kde.org/images/screenshots/okteta.png"
groups=("kde" "kde-devel" "kdesdk" "kde-uninstall")
depends=('kcmutils' 'kparts' 'knewstuff' 'qca-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'qt5-tools' 'hicolor-icon-theme')
replaces=('kdesdk-okteta')
conflicts=('kdesdk-okteta')
provides=('kdesdk-okteta')
license=('GPL' 'LGPL' 'FDL')
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('29dc30fd71dfc5337c7d2f814feed1ced5799337c5b1fb5098539ba30941a490'
            'SKIP')
validpgpkeys=(E191FD5BE6F46870F09E82B2024E7FB43D015474  # Friedrich W. H. Kossebau <kossebau@kde.org>
              0A48BC961075B4BA8523E3790A345FB086E797D9) # https://share.kde.org/s/8dKaJTw8pR8iiwJ

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
