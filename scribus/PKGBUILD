pkgname=scribus
pkgver=1.5.5
pkgrel=1
pkgdesc="A desktop publishing program"
arch=('x86_64')
url="https://www.scribus.net"
license=('GPL')
depends=(qt5-base harfbuzz-icu poppler python2 hunspell podofo openscenegraph
         libcdr libfreehand libmspub libpagemaker libqxp libvisio libzmf)
makedepends=('cmake' 'boost' 'mesa' 'qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('7908b21a6ce843269f58cedf5f8f791893257e6201cce5fbddc70daca2fe3f71')

prepare() {
    cd ${pkgname}-${pkgver}

    # Fix python name
    sed \
        -e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
        -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
        -i scribus/plugins/scriptplugin/{samples,scripts}/*
}

build() {
	mkdir -p build
        cd build
	
	cmake ../$pkgname-$pkgver \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWITH_ASPELL=0 \
           -DWANT_HUNSPELL=1 \
	   -DWANT_CAIRO=1 \
	   -DLIB_SUFFIX=""
	make 
}

package() {
	cd build
	
	make DESTDIR=$pkgdir install
	
	install -Dm644 $srcdir/$pkgname-$pkgver/scribus.desktop $pkgdir/usr/share/applications/scribus.desktop
        for i in 16x16 32x32 128x128 256x256 512x512 1024x1024
    	do
        	install -Dm644 $srcdir/$pkgname-$pkgver/resources/iconsets/artwork/icon_${i}.png $pkgdir/usr/share/icons/hicolor/${i}/apps/scribus.png
    	done

    	# Use system hyphen
    	rm -rf $pkgdir/usr/share/scribus/dicts/hyph
    	ln -sf /usr/share/hyphen $pkgdir/usr/share/scribus/dicts/hyph
}
