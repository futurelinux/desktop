# Contributions from Arch Linux: https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=unknown-horizons
# Contributions from AUR: https://aur.archlinux.org/packages/unknown-horizons/

pkgname=unknown-horizons
pkgver=2019.1
pkgrel=1
pkgdesc="2D realtime strategy simulation with an emphasis on economy and city building."
arch=('any')
url="http://www.unknown-horizons.org/"
license=('GPL' 'CCPL')
depends=('fife' 'python3' 'python3-pillow' 'python3-yaml' 'python3-future')
makedepends=('intltool' 'python3-setuptools')
conflicts=('unknown-horizons-svn' 'unknown-horizons-git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('87ae37442ee0b80130c7a75c7eeb7c0ef6c50f3e6b2616db32e6381ace5068729ab7086edefc00eefd3b89e85f9a05fb6c4d05f2a2adc46fa928e5c2a29b72db')

# unknown-horizons build system misses a few files if the build step is seperated from the install
# step
build() {
    cd "$srcdir/$pkgname-$pkgver"
    python3 horizons/engine/generate_atlases.py 2048
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    python3 setup.py install --root="$pkgdir/" --optimize=1
}
