# Contributions from AUR: https://aur.archlinux.org/packages/fife/

pkgname=fife
pkgver=0.4.2
pkgrel=2
_githubname=fifengine
pkgdesc="Flexible Isometric Free Engine is a cross platform game creation framework"
arch=(x86_64)
url="http://fifengine.net/"
license=('LGPL')
depends=('boost' 'fifechan' 'libvorbis' 'sdl2_ttf' 'openal' 'python3' 'tinyxml' 'glew')
makedepends=('cmake' 'mesa' 'swig')
source=("https://github.com/${_githubname}/${_githubname}/archive/${pkgver}.tar.gz")
sha256sums=('e882b55befa36ae2f1a89ada16af0f814a1b78f7cbe6021ec5b1605604cf8ff8')

build() {
  cd "${_githubname}-${pkgver}"
  [[ -d "build" ]] && rm -r "build"
  mkdir -p "build" && cd "build"
  cmake \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DPython_ADDITIONAL_VERSIONS=3 \
    ..
  make
  # If compilation seems to stall at 99% for ages, do not abort!
  # While not perfectly convenient, that is to be expected from
  # SWIG. Eventually, you'll reach the holy triple-digit land.
}

package() {
  cd "${_githubname}-${pkgver}"
  cd "build"
  make DESTDIR="$pkgdir" install
}
