# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-build-tools
pkgver=0.6.0
pkgrel=1
pkgdesc="Various packaging tools and scripts for LXQt applications"
arch=("any")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("cmake" "qt5-tools" "git")
source=("https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc")
sha256sums=('2488f1105ba8008996b4f6a0df5c556c657c733a47a422ea3f2e59115c051758'
            'SKIP')
validpgpkeys=('169704C6FB490C6892C7F23C37E0AF1FDA48F373'  # Jerome Leclanche <jerome@leclan.ch>
	      '7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3')  # Alf Gaida <agaida@siduction.org>


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
