pkgname=dianara
pkgver=1.4.3
pkgrel=1
pkgdesc="A Qt pump.io client"
arch=('x86_64')
url="http://dianara.nongnu.org/"
license=('GPL')
depends=('qoauth' 'qca' 'qca-ossl' 'sonnet' 'kwidgetsaddons')
source=("https://download-mirror.savannah.gnu.org/releases/$pkgname/$pkgname-v$pkgver.tar.gz")
sha256sums=('6229fd940c6cbc62e310c41bc010ce763c604bd2aa24a82d0055be8ac5d91f8f')

build() {
  cd $pkgname-v$pkgver
  [ -d bld ] || mkdir bld && cd bld
  qmake-qt5 ..
  sed -i 's|include5|include|' Makefile
  make
}

package() {
  cd $pkgname-v$pkgver

  # bin
  install -Dm755 bld/$pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 org.nongnu.dianara.desktop \
    "$pkgdir/usr/share/applications/org.nongnu.dianara.desktop"

  # icons
  install -Dm644 icon/32x32/$pkgname.png \
    "$pkgdir/usr/share/icons/hicolor/32x32/apps/$pkgname.png"
  install -Dm644 icon/64x64/$pkgname.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/$pkgname.png"

  # translations
  install -d "$pkgdir/usr/share/locale"
  install -Dm644 translations/*.qm \
    "$pkgdir/usr/share/locale"

  # man
  install -Dm644 manual/$pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1"
}
