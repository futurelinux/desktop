pkgbase=shiboken2
pkgname=('shiboken2' 'python-shiboken2')
_qtver=5.12.4
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io/'
license=('GPL2' 'LGPL')
pkgdesc='Generates bindings for C++ libraries using CPython source code'
makedepends=('clang' 'llvm' 'cmake' 'libxslt' 'qt5-xmlpatterns' 'python3-sphinx')
_pkgfqn=pyside-setup-everywhere-src-${_qtver}
source=("https://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz")
sha256sums=('4a64ed11eb22af46db02615f9b1f597de0feaf8555cf41f355ec2b366d5bc2b4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgfqn}/sources/shiboken2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DUSE_PYTHON_VERSION=3
  make
}

package_shiboken2() {
  depends=('clang' 'libxslt' 'qt5-xmlpatterns')

  cd build
  make DESTDIR="$pkgdir" install
  # Provided in python-shiboken2
  rm -r "$pkgdir"/usr/lib/{python*,libshiboken*}
}

package_python-shiboken2() {
  depends=('python3')

  cd build
  make DESTDIR="$pkgdir" install
  # Provided in shiboken2
  rm -r "$pkgdir"/usr/{bin,include,lib/{cmake,pkgconfig},share}
} 
