# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Morten Linderud <foxboron@archlinux.org>
# Contributor: Karol Babioch <karol@babioch.de>

pkgname='yubikey-manager'
pkgver=2.0.0
pkgrel=1
pkgdesc='Python library and command line tool for configuring a YubiKey'
arch=('any')
url='https://developers.yubico.com/yubikey-manager/'
license=('BSD')
depends=('python3' 'python3-click' 'python3-cryptography' 'python3-pyopenssl' 'python3-pyscard'
         'python3-pyusb' 'python3-six' 'python-fido2' 'ccid' 'libu2f-host'
         'yubikey-personalization')
makedepends=('python3-setuptools')
validpgpkeys=('8D0B4EBA9345254BCEC0E843514F078FF4AB24C3'  # Dag Heyman <dag@yubico.com>
              '57A9DEED4C6D962A923BB691816F3ED99921835E') # Emil Lundberg (Software Developer) <emil@yubico.com>
source=("https://developers.yubico.com/$pkgname/Releases/yubikey-manager-$pkgver.tar.gz"{,.sig})
sha256sums=('e95b4c4e956e105780e59ca2e4f159b4e974da38cdc810d4157e8d979ebf66f4'
            'SKIP')

build() {
	cd "${pkgname}-${pkgver}"
	python3 setup.py build
}

package() {
	cd "${pkgname}-${pkgver}"
	python3 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

