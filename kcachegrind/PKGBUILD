# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=kcachegrind
pkgver=19.08.2
pkgrel=3
arch=(x86_64)
pkgdesc="KDE profiler visualization"
screenshot="http://www.kde.org/images/screenshots/kcachegrind.png"
groups=(kde-applications kdesdk)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python3 qt5-tools)
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
conflicts=(kdesdk-kcachegrind)
replaces=(kdesdk-kcachegrind)
provides=(kdesdk-kcachegrind)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('baf17a5c11f21deb7b019a7e3a9819348ec8d20af5c8c4a6108b96266e425b46'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
