# Contributions from ArchLinux: https://git.archlinux.org/svntogit/community.git/tree/trunk?h=packages/usb_modeswitch

pkgname=usb-modeswitch
_pkgname=usb_modeswitch
pkgver=2.6.0
pkgrel=1
_pkgdata=20191128
pkgdesc="Activating switchable USB devices on Linux."
arch=('x86_64')
url="http://www.draisberghof.de/usb_modeswitch/"
license=('GPL3')
depends=('libusb' 'tcl')
makedepends=('gcc' 'make')
conflicts=(usb_modeswitch)
replaces=(usb_modeswitch)
categories=('system')
backup=("etc/$_pkgname.conf")
source=("http://www.draisberghof.de/$_pkgname/$pkgname-$pkgver.tar.bz2"
	"http://www.draisberghof.de/$_pkgname/$pkgname-data-$_pkgdata.tar.bz2"
	"usb-modeswitch-${pkgver}-device_reference.txt::http://www.draisberghof.de/$_pkgname/device_reference.txt")
sha256sums=('c215236e6bada6e659fc195a31d611ea298a4bdb4d57a0d68c553b56585f8ba3'
            '3f039b60791c21c7cb15c7986cac89650f076dc274798fa242231b910785eaf9'
            '846b45a82a31c9755ab86d9c051e706aff445e1e793575b7ae195a4edeb4d594')
          
prepare() {
  cd "$srcdir"/usb-modeswitch-$pkgver/
  sed -i 's|/usr/sbin/usb_mode|/usr/bin/usb_mode|g' usb_modeswitch{.conf,_dispatcher.tcl,.sh,@.service}
}
build() {
  cd "$srcdir"/usb-modeswitch-$pkgver/
  make clean
  make SBINDIR=/usr/bin
}

package() {
  cd "$srcdir"/usb-modeswitch-$pkgver/

  mkdir -p "$pkgdir"/usr/lib/udev
  mkdir -p "$pkgdir"/etc
  mkdir -p "$pkgdir"/usr/share/man/man1
  mkdir -p "$pkgdir"/usr/lib/systemd/system

  make \
    DESTDIR="$pkgdir" \
    UDEVDIR="$pkgdir"/usr/lib/udev \
    SBINDIR="$pkgdir"/usr/bin \
    SYSDIR="$pkgdir"/usr/lib/systemd/system \
    install

  cp "$srcdir"/usb-modeswitch-${pkgver}-device_reference.txt "$pkgdir"/etc/usb_modeswitch.setup

  cd "$srcdir"/usb-modeswitch-data-${_pkgdata}
  make DESTDIR="$pkgdir" install RULESDIR="$pkgdir"/usr/lib/udev/rules.d
}
