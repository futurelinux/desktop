pkgname=musescore
_pkgname=MuseScore
pkgver=3.3.4
pkgrel=1
pkgdesc="A music score editor written in Qt"
arch=('x86_64')
url="https://musescore.org/en/"
license=('GPL')
depends=('alsa-lib' 'freetype2' 'libpulse' 'libsndfile' 'libvorbis' 'portaudio' 'portmidi' 'qt5-base' 'qt5-declarative'
         'qt5-graphicaleffects' 'qt5-quickcontrols2' 'qt5-svg' 'qt5-tools' 'qt5-webengine' 'qt5-xmlpatterns' 'zlib')
makedepends=('cmake' 'doxygen' 'lame' 'qt5-script' 'texlive-core')
optdepends=('lame: MP3 export')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$_pkgname/archive/v$pkgver.tar.gz")
sha256sums=('ab8bade6c7eec6a9ae03f435cad65e79c4ca031d19020aa6e2a4da2868f49a2a')

prepare() {
 cd $_pkgname-$pkgver
 sed 's,QT_QMAKE_EXECUTABLE qmake,QT_QMAKE_EXECUTABLE qmake-qt5,g' -i build/FindQt5.cmake
}

build() {
  cd $_pkgname-$pkgver
  export CI=False
  make PREFIX='/usr' NO_RPATH=TRUE USE_SYSTEM_FREETYPE="ON" UPDATE_CACHE="OFF" release
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR=$pkgdir -C build.release install
}
