# Contribution from Arch: https://git.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/calligra

pkgname=calligra
pkgdesc="A set of applications for productivity and creative usage"
pkgver=3.2.0
pkgrel=1
arch=('x86_64')
url='https://calligra.org'
license=('FDL1.2' 'GPL2' 'LGPL')
depends=('kcmutils' 'knotifyconfig' 'kross' 'kactivities' 'kdiagram' 'libspnav' 'kdelibs4support'
         'gsl' 'openexr' 'kcontacts' 'qca' 'poppler-qt5' 'libodfgen' 'khtml' 'cauchy')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdesignerplugin' 'kinit' 'libwpg' 'okular' 'eigen3' 'marble'
             'boost' 'pstoedit' 'libvisio' 'libetonyek' 'libwps' 'vc' 'libgit2' 'kcalendarcore' 'akonadi')
optdepends=('libwpg: Corel WordPerfect Graphics image importer'
            'libwps: Microsoft Works file word processor format import'
            'libvisio: Microsoft Visio import filter'
            'libetonyek: Apple Keynote import filter'
            'pstoedit: EPS to SVG filter'
            'poppler: PDF to SVG filter'
            'libgit2: Calligra Gemini git plugin'
            'kirigami: for Calligra Gemini'
            'qt5-quickcontrols: for Calligra Gemini'
            'qt5-webengine: for Calligra Gemini')
options=("debug")

# list of replaced packages
conflicts=('calligra-braindump'
         'calligra-devtools'
         'calligra-extras'
         'calligra-filters'
         'calligra-flow'
         'calligra-gemini'
         'calligra-handbook'
         'calligra-karbon'
         'karbon'
         'calligra-libs'
         'calligra-plugins'
         'calligra-sheets'
         'calligra-stage'
         'calligra-words'
         'calligra-l10n')
replaces=('calligra-braindump'
         'calligra-devtools'
         'calligra-extras'
         'calligra-filters'
         'calligra-flow'
         'calligra-gemini'
         'calligra-handbook'
         'calligra-karbon'
         'karbon'
         'calligra-libs'
         'calligra-plugins'
         'calligra-sheets'
         'calligra-stage'
         'calligra-words'
         'calligra-l10n')
for _lang in bs ca ca@valencia cs da de el en_GB es et fi fr gl hu it ja kk nb nl \
             pl pt pt_BR ru sk sv uk zh_CN zh_TW; do
  conflicts+=(calligra-l10n-$_lang)
  replaces+=(calligra-l10n-$_lang)
done

source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('7e556228665d79e67c062fd1173d6676f0b0d7ceefdd6e1b0bcf9a781ea70951'
            'SKIP')
validpgpkeys=(05D00A8B73A686789E0A156858B9596C722EA3BD
              42A60D06F449E895F40F09C01638B8A616108B8A) # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
