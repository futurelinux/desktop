#contributions from Arch: https://www.archlinux.org/packages/community/x86_64/mixxx/

pkgname=mixxx
pkgver=2.2.3
pkgrel=1
pkgdesc="Free, open source software for digital DJ'ing."
arch=('x86_64')
url="https://www.mixxx.org/"
license=('GPL2')
depends=('chromaprint' 'gcc-libs' 'glibc' 'glib2' 'hidapi' 'faad2'
         'flac' 'libglvnd' 'libid3tag' 'lilv' 'libmad' 'libmodplug'
         'libmp4v2' 'libogg' 'portaudio' 'portmidi' 'protobuf'
         'rubberband' 'libshout' 'libsndfile' 'gperftools' 'libusb'
         'libvorbis' 'libx11' 'opusfile' 'qt5-base' 'qt5-script' 'qt5-svg'
         'qt5-x11extras' 'qtkeychain' 'soundtouch' 'sqlite' 'taglib' 'upower' 'wavpack')
makedepends=('glu' 'lv2' 'qt5-tools' 'scons' 'vamp-plugin-sdk')
source=("https://github.com/${pkgname}dj/${pkgname}/archive/release-${pkgver}.tar.gz")
sha256sums=('69230dca511ba08a284a22a979ff05b62130e97156d39a044bb4045f5f43cd2e')

build() {
  cd "${srcdir}/${pkgname}-release-${pkgver}"
  export SCONSFLAGS="-j $(nproc)"
  scons prefix=/usr \
        build=release \
        faad=1 \
        ffmpeg=1 \
        modplug=1 \
        perftools=1 \
        perftools_profiler=1 \
        qt_sqlite_plugin=0 \
        qtkeychain=1 \
        target=linux \
        virtualize=0 \
        wv=1
}

package() {
  cd "${srcdir}/${pkgname}-release-${pkgver}"
  scons prefix=/usr \
        install_root="${pkgdir}/usr" \
        install
}
