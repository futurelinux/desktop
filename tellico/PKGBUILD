pkgname=tellico
pkgver=3.2.3
pkgrel=1
pkgdesc="A collection manager for KDE"
arch=('x86_64')
url="https://tellico-project.org/"
license=('GPL')
depends=('exempi' 'kio' 'libksane' 'libkcddb' 'libcddb' 'libdiscid' 'hicolor-icon-theme' 'kfilemetadata'
         'knewstuff' 'kxmlgui' 'kitemmodels' 'python2' 'solid' 'khtml' 'yaz' 'taglib' 'poppler-qt5' 'qt5-base')
makedepends=('cmake' 'extra-cmake-modules' 'kdoctools' 'libkcddb')
categories=('office')
source=(https://tellico-project.org/files/${pkgname}-${pkgver}.tar.xz)
sha256sums=('f4456eb31f71151b9786fa171531265b421b2de8140e9adbf6bb584862c42952')

build() {
  cd "${srcdir}"

  mkdir -p build && cd build

  cmake ../${pkgname}-${pkgver} -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

# fix python 2.7 path
  find "${pkgdir}" -iname "*.py" | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
