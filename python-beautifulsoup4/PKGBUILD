pkgbase=python-beautifulsoup4
pkgname=('python2-beautifulsoup4' 'python3-beautifulsoup4')
pkgver=4.7.1
pkgrel=1
pkgdesc="A Python HTML/XML parser designed for quick turnaround projects like screen-scraping."
arch=('x86_64')
url="http://www.crummy.com/software/BeautifulSoup/index.html"
license=('PSF')
makedepends=('python2-setuptools' 'python3-setuptools' 'python3-soupsieve' 'python2-soupsieve')
source=("http://www.crummy.com/software/BeautifulSoup/bs4/download/${pkgver%.*}/${pkgbase#*-}-${pkgver}.tar.gz")
sha512sums=('7aa77bc6008bbcbbbe91b0a850007ab237d2832b63a787fbd94b7cbf47d4276b185e0c61c134df73221406458edff2b75b6b8c2b53b543aa3bb1b0e2202dac5a')
 
build() {
  cd "${srcdir}"
  cp -a beautifulsoup4-${pkgver}{,-python2}

  # Build python 3 module
  cd "${srcdir}/beautifulsoup4-${pkgver}"
  python3 setup.py build

  # Build python 2 module
  cd "${srcdir}/beautifulsoup4-${pkgver}-python2"
  python2 setup.py build
}

package_python2-beautifulsoup4() {
  depends=('python2-soupsieve')
  optdepends=('python2-chardet: universal encoding detector'
              'python2-lxml: pythonic binding for the libxml2 and libxslt libraries'
              'python2-html5lib: alternative HTML parser')

  cd "${srcdir}/beautifulsoup4-${pkgver}-python2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
}

package_python3-beautifulsoup4() {
  depends=('python3-soupsieve')
  optdepends=('python3-chardet: universal encoding detector'
              'python3-lxml: pythonic binding for the libxml2 and libxslt libraries'
              'python3-html5lib: alternative HTML parser')

  cd "${srcdir}/beautifulsoup4-${pkgver}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
}
