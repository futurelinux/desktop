# Contributions from Arch:
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gwenhywfar
pkgver=5.1.2
_verid=234
pkgrel=1
pkgdesc="OS abstraction functions for various projects"
arch=(x86_64)
url="https://www.aquamaniac.de/"
license=(LGPL)
depends=(gnutls libgcrypt openssl)
makedepends=(git qt5-base)
optdepends=('qt5-base: for the Qt5 UI')
source=("https://www.aquamaniac.de/rdm/attachments/download/$_verid/$pkgname-$pkgver.tar.gz")
sha256sums=('e2eae2a50e83512664216230caebafbc170c5806a6f0130b00e44f2b6a5a5441')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-ssl \
              --with-guis="qt5" \
              --with-qt5-qmake=/usr/lib/qt5/bin/qmake \
              --with-qt5-moc=/usr/lib/qt5/bin/moc \
              --with-qt5-uic=/usr/lib/qt5/bin/uic
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  sed -i 's|gnutls_transport_set_lowat||g' src/sio/syncio_tls.c
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
