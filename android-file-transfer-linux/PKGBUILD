pkgname=android-file-transfer-linux
pkgver=3.9
pkgrel=1
pkgdesc="An interactive MTP client implemented in C++ using Qt toolkit"
arch=('x86_64')
url="https://github.com/whoozle/android-file-transfer-linux"
license=('LGPL')
depends=('qt5-base' 'fuse2' 'hicolor-icon-theme' 'libxkbcommon-x11' 'file')
makedepends=('cmake')
source=("https://github.com/whoozle/android-file-transfer-linux/archive/v$pkgver.tar.gz"
        'solid_aftl.desktop')
sha256sums=('26aa21683ae34aceaf83747fec693a15b996017a857014931ef3c5b50115971a'
            '743a0099e38fd82443b51434b65e65f0a64b2791bd2af89392f4f4d991c3bfc9')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  
  # added plasma5 integration
  install -D ../../solid_aftl.desktop $pkgdir/usr/share/solid/actions/solid_aftl.desktop
}
