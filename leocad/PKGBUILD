pkgname=leocad
pkgver=18.02
pkgrel=2
pkgdesc="A CAD program for creating virtual LEGO models"
arch=('x86_64')
url="http://leocad.org"
license=('GPL')
depends=('qt5-base' 'hicolor-icon-theme')
makedepends=('qt5-tools')
_libver=11331
source=("$pkgname-$pkgver.tar.gz::https://github.com/leozide/leocad/archive/v$pkgver.tar.gz"
        "https://github.com/leozide/leocad/releases/download/v$pkgver/Library-Linux-$_libver.zip")
sha256sums=('496567a9f05930b3366b38053b6de4f2c9d917280079eeb45d08581784903ca1'
            '55063566f8c887b04f280e32aa138fee2388c267bfc5e7b0f51d5cdcff54c9b4')

build() {
  cd $pkgname-$pkgver

  qmake-qt5 \
    INSTALL_PREFIX=/usr \
    DISABLE_UPDATE_CHECK=1
  make
}

package() {
  cd $pkgname-$pkgver

  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 ../library.bin "$pkgdir"/usr/share/leocad/library.bin
}
