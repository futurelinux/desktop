pkgname=stellarium
pkgver=0.19.2
pkgrel=1
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=('x86_64')
url="https://stellarium.org/"
screenshot=('http://www.stellarium.org/img/screenshots/0.10-from-mars.jpg')
license=('GPL2')
depends=('libpng' 'libgl' 'glu' 'openssl' 'zlib' 'gettext' 'qt5-script'
         'xdg-utils' 'freetype2' 'phonon-qt5' 'qt5-multimedia' 'desktop-file-utils'
         'qt5-serialport' 'qt5-location')
makedepends=('cmake' 'boost' 'mesa' 'qt5-tools')
categories=('education')
source=("https://github.com/Stellarium/stellarium/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('e4d005c5473e3be15b3101585e8959f422437147a602b1649e4aba22a92e6081')

build() {
  cd ${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
 
  make DESTDIR="${pkgdir}" install

  # install desktop file
  install -Dm644 data/stellarium.desktop \
	"${pkgdir}/usr/share/applications/stellarium.desktop"

  # install desktop icons
  for i in 16 32 48 64 128 256 512; do
    install -Dm644 data/icons/${i}x${i}/stellarium.png \
      "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/stellarium.png"
  done    
}
