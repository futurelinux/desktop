pkgname=kpmcore
pkgver=4.0.0
pkgrel=1
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('x86_64')
url="http://kde.org/applications/system/kdepartitionmanager/"
license=('GPL2')
depends=('smartmontools' 'kwidgetsaddons' 'ki18n' 'kauth' 'qca')
makedepends=('extra-cmake-modules' 'python3')
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfs-3g: NTFS support'
	    'dosfstools: FAT32 support'
	    'fatresize: FAT resize support'
	    'f2fs-tools: F2FS support'
	    'btrfs-progs: btrfs support'
	    'exfat-utils: exFAT support'
	    'nilfs-utils: nilfs support')
source=("https://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d7f044ca256369bec62ec5460bad397cd67473f4a8b2d48686e91a9bf24ddf6d')

build() {
  cd ${pkgname}-${pkgver}

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
