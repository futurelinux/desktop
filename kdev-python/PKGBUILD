pkgname=kdev-python
pkgver=5.3.3
pkgrel=1
pkgdesc="Python language and documentation plugin for KDevelop"
arch=('x86_64')
url="http://www.kdevelop.org/"
license=(GPL)
depends=(kdevelop python3 knewstuff threadweaver)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/kdevelop/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('80c4bab96aede589ebdb6d4b33d0339766ac645cc0e4fe10674df12008f37f04'
            'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6'
              '364EFA5F639572907D392999C64CF56B13CACE5D'
              'E191FD5BE6F46870F09E82B2024E7FB43D015474')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake  ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
