# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=appstream-glib
pkgver=0.7.15
pkgrel=2
pkgdesc="Objects and methods for reading and writing AppStream metadata"
url="https://people.freedesktop.org/~hughsient/appstream-glib/"
arch=(x86_64)
license=(LGPL)
depends=(libyaml pacman gcab libsoup libstemmer json-glib gdk-pixbuf2)
makedepends=(gobject-introspection autoconf-archive git gperf meson cmake)
_commit=0ab28ff96b74eb72ffe3c3a53026aee82cf31782  # tags/appstream_glib_0_7_15^0
source=("git+https://github.com/hughsie/appstream-glib#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^appstream_glib_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  chakra-meson $pkgname build \
    -D alpm=true \
    -D gtk-doc=false \
    -D fonts=false \
    -D rpm=false
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
