# Arch Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=go-tools
pkgver=1.14+3922+575de4798
pkgrel=1
pkgdesc='Developer tools for the Go programming language'
arch=(x86_64)
url='http://golang.org/'
license=(BSD)
depends=(glibc)
makedepends=(git go-pie)
_commit=575de47986ce07f39826bf8d5e365d5ebf51ee63
_net_commit=ef20fe5d793301b553005db740f730d87993f778
source=(git+https://go.googlesource.com/tools#commit=$_commit
        git+https://github.com/golang/net#commit=$_net_commit)
md5sums=('SKIP'
         'SKIP')

_tools=(benchcmp callgraph compilebench cover digraph eg fiximports
        go-contrib-init godex godoc goimports gomvpkg gorename gotype 
        goyacc guru html2article present ssadump stringer gopls
        toolstash)

pkgver() {
  local _gover=1.14
  cd tools
  printf "%s+%s+%s" $_gover "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p gopath/src/golang.org/x
  cp -rf tools net gopath/src/golang.org/x/
  cp -rf net gopath/src/golang.org/x/net
}

build() {
  export GOPATH="$srcdir/gopath"
  cd gopath/src/golang.org/x/tools
 go install -v -a -trimpath -ldflags "-extldflags ${LDFLAGS}" ./cmd/...
}

check() {
  export GOPATH="$srcdir/gopath"
  cd gopath/src/golang.org/x/tools
  go test ./cmd/... || true
}

package() {
  for tool in ${_tools[@]}; do
    install -Dm755 gopath/bin/$tool "$pkgdir/usr/bin/$tool"
  done

  install -Dm644 tools/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

