pkgname=scummvm
pkgver=2.1.1
pkgrel=1
pkgdesc="A virtual machine for several classic graphical point-and-click adventure games."
arch=('x86_64')
license=('GPL3')
url="http://www.scummvm.org/"
depends=('libpng' 'libtheora' 'sdl2' 'sdl2_net' 'fluidsynth' 'flac' 'faad2' 'libvorbis' 'libmad' 'freetype2'
         'libgl' 'glu' 'libjpeg-turbo' 'libmpeg2' 'curl' 'a52dec')
makedepends=('mesa')
source=("https://scummvm.org/frs/scummvm/${pkgver}/scummvm-${pkgver}.tar.xz"
	fluidsynth2_a.patch
        fluidsynth2_b.patch)
sha256sums=('5753769356f3396fde1e614b40496c61392a2903c53ca436b721dde58353dca8'
            '369e1b7de468b7758d2ab4c568a82977aabf2db45e9ef8fd7c97fec053fada23'
            'dd19a18e0ba605cba32d39e1832d73243ccf2b00fe649befa79e98611a8076bc')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}"/fluidsynth2_a.patch
  patch -Np1 -i "${srcdir}"/fluidsynth2_b.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure \
    --enable-c++11 \
    --enable-release \
    --prefix=/usr \
    --disable-debug
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 dists/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
}


