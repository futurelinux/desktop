# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cbindgen
pkgver=0.9.1
pkgrel=1
pkgdesc="A tool for generating C bindings to Rust code"
url="https://github.com/eqrion/cbindgen"
arch=(x86_64)
license=(MPL2)
depends=(gcc-libs)
makedepends=(rust git llvm-libs)
_commit=8e4db4c17fbdc0cfa9b98cfe9d47ca6263858def  # tags/v0.9.1^0
source=("git+https://github.com/eqrion/cbindgen#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  cargo build --release
}

check() {
  cd $pkgname
  cargo test --release || :
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/bin" target/release/cbindgen
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README.md
}
