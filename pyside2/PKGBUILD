pkgname=pyside2
_qtver=5.12.4
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64')
url="https://wiki.qt.io/Qt_for_Python"
license=(LGPL)
pkgdesc='Enables the use of Qt5 APIs in Python applications'
depends=('python-shiboken2' 'qt5-base')
makedepends=('shiboken2' 'cmake' 'llvm' 'clang'
            'qt5-multimedia' 'qt5-tools' 'qt5-sensors' 'qt5-charts' 'qt5-webengine' 'qt5-datavis3d'
            'qt5-websockets' 'qt5-speech' 'qt5-3d' 'qt5-svg' 'qt5-script' 'qt5-scxml' 'qt5-x11extras' 'qt5-remoteobjects')
optdepends=('qt5-svg: QtSvg bindings'
            'qt5-script: QtScript bindings'
            'qt5-speech: QtTextToSpeech bindings'
            'qt5-websockets: QtWebSockets bindings'
            'qt5-webengine: QtWebEngine bindings'
            'qt5-datavis3d: QtDataVisualization bindings'
            'qt5-scxml: QtScxml bindings'
            'qt5-sensors: QtSensors bindings'
            'qt5-3d: Qt3D bindings'
            'qt5-x11extras: QtX11Extras bindings'
            'qt5-charts: QtCharts bindings'
            'qt5-tools: QtHelp bindings'
            'qt5-remoteobjects: QtRemoteObjects bindings')
provides=(python-pyside2)
source=("https://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/pyside-setup-everywhere-src-${_qtver}.tar.xz")
sha256sums=('4a64ed11eb22af46db02615f9b1f597de0feaf8555cf41f355ec2b366d5bc2b4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../pyside-setup-everywhere-src-${_qtver}/sources/pyside2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python3 \
    -DCMAKE_BUILD_TYPE=None # https://bugreports.qt.io/browse/PYSIDE-980
  make
}

package() {
  cd build

  make DESTDIR=$pkgdir install
}
