# Contributions from Arch:
# Maintainer: Konstantin Shalygin <k0ste@k0ste.ru>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgname=nextcloud-client
pkgver=2.6.0
pkgrel=1
pkgdesc='Nextcloud desktop client'
arch=('x86_64')
url='https://nextcloud.com/'
license=('GPL2')
depends=('qt5-webengine' 'qt5-webkit' 'hicolor-icon-theme' 'xdg-utils' 'qtkeychain')
makedepends=('extra-cmake-modules' 'python3-sphinx' 'qt5-tools' 'doxygen' 'kio')
optdepends=('kio: Resource and network access abstraction (KDE)')
conflicts=('owncloud-client')
replaces=('owncloud-client')
source=("https://github.com/nextcloud/desktop/archive/v${pkgver}.tar.gz")

backup=('etc/Nextcloud/sync-exclude.lst')

prepare() {
  mkdir -p desktop-$pkgver/build
}

build() {
  cd desktop-$pkgver/build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
  make doc-man
}

package() {
  cd desktop-$pkgver/build
  make DESTDIR=$pkgdir install
}
md5sums=('1f63e129cb24518822851a8a8ce9d12f')
