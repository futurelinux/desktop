pkgbase=python-pillow
pkgname=(python3-pillow python2-pillow)
pkgver=6.0.0
pkgrel=1
_appname=Pillow
_py2basever=2.7
_py3basever=3.6m
pkgdesc="Python Imaging Library (PIL) fork. Python3 version."
arch=('x86_64')
url="https://python-pillow.org/"
license=('BSD')
depends=('lcms2' 'libtiff' 'openjpeg2' 'libimagequant')
optdepends=('freetype2: for the ImageFont module'
            'libraqm: for complex text scripts'
            'libwebp: for webp images'
            'tk: for the ImageTK module')
makedepends=('python3-setuptools' 'python2-setuptools' 'python3' 'python2' 'freetype2' 'libraqm' 'libwebp' 'tk')
source=("https://files.pythonhosted.org/packages/source/P/$_appname/$_appname-$pkgver.tar.gz")
md5sums=('9da5d2db0d7604ba553ece1ab7ca53a3')

prepare() {
  cd "$srcdir/${_appname}-$pkgver"
}

build() {
  cd "$srcdir/${_appname}-$pkgver"
  python3 setup.py build
  python2 setup.py build
}

package_python3-pillow() {
  depends+=('python3')
  optdepends+=('python3-pyqt5: for the ImageQt module')
  cd "$srcdir/$_appname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py3basever/"
  install -m644 -t "$pkgdir/usr/include/python$_py3basever/" src/libImaging/*.h
}

package_python2-pillow() {
  pkgdesc="Python Imaging Library (PIL) fork. Python2 version."
  depends+=('python2')
  optdepends+=('python2-pyqt5: for the ImageQt module')
  provides=('python-imaging' 'python2-imaging')
  conflicts=('python-imaging' 'python2-imaging')
  cd "$srcdir/${_appname}-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py2basever/"
  install -m644 -t "$pkgdir/usr/include/python$_py2basever/" src/libImaging/*.h
}
