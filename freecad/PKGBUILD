pkgname=freecad
_pkgname=FreeCAD
pkgver=0.18.4
pkgrel=2
pkgdesc='A general purpose 3D CAD modeler'
arch=('x86_64')
url='https://www.freecadweb.org/wiki'
license=('GPL')
depends=('boost-libs' 'curl' 'opencascade' 'xerces-c' 'libspnav' 'glew'
         'shared-mime-info' 'hicolor-icon-theme' 'jsoncpp' 'med' 'qt5-base' 'qt5-declarative' 'qt5-tools' 'qt5-svg' 'netcdf'
         'pyside2' 'pyside2-tools' 'python3-matplotlib' 'python-shiboken2'
         'shiboken2' 'coin' 'doxygen' 'qt5-x11extras')
makedepends=('boost' 'eigen3' 'gcc-fortran' 'swig' 'xerces-c'
             'desktop-file-utils' 'cmake' 'coin' 'zlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FreeCAD/FreeCAD/archive/$pkgver.tar.gz"
	"qt5-lesser-than-14.patch::https://github.com/FreeCAD/FreeCAD/commit/1666b28836baa79b26b707afb1e97c2a0855eb1f.patch"
	"find-pyside2.patch::https://github.com/FreeCAD/FreeCAD/commit/17ed2b196a58afbbc3cc1bb99f6eb76060171ad2.patch"
        "${pkgname}.desktop"
	"${pkgname}.xml")
sha256sums=('4e0cce447b31b8989a00cf68c49ae012ce8e5546a56c6e0874fbd8f7ddedffd2'
            'a74b2f0a48056f73d4b5adaf37bb71750b140bb05a4e417b8c7ed35f294facde'
            '9af1f0b85a58acad3035fd5abfaf60f57f6a5424101cb328f16468a9e4aebfa5'
            '321ee7669ad8cce5dd6d7d352b11952c18b37e4b178c94636a3b08079a3e27cb'
            '248918de7d3c2145b5cc4fbbc9e224d22f4a6ca7ead2680e8c3a32e91772482a')
prepare() {
  cd FreeCAD-$pkgver
  patch -Np1 -i ../find-pyside2.patch
  patch -Np1 -i ../qt5-lesser-than-14.patch
}

build() {
  cd FreeCAD-$pkgver
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="/usr/lib/freecad" \
    -DCMAKE_INSTALL_DOCDIR="/usr/share/freecad/doc" \
    -DCMAKE_INSTALL_DATADIR="/usr/share/freecad" \
    -DFREECAD_USE_OCC_VARIANT="Official Version" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3 \
    -DBUILD_QT5=ON \
    -DBUILD_QT5_WEBKIT=OFF \
    -DBUILD_START=OFF \
    -DBUILD_WEB=OFF 
  make
}

package() {
  cd FreeCAD-$pkgver
  make DESTDIR=$pkgdir install

  # Symlink to /usr/bin
    install -dm755 "${pkgdir}/usr/bin"
    ln -srf "${pkgdir}/usr/lib/freecad/bin/FreeCAD" "${pkgdir}/usr/bin/freecad"
    ln -srf "${pkgdir}/usr/lib/freecad/bin/FreeCAD" "${pkgdir}/usr/bin/FreeCAD"
    ln -srf "${pkgdir}/usr/lib/freecad/bin/FreeCADCmd" "${pkgdir}/usr/bin/freecadcmd"
    ln -srf "${pkgdir}/usr/lib/freecad/bin/FreeCADCmd" "${pkgdir}/usr/bin/FreeCADCmd"

    # Install pixmaps and desktop shortcut
    desktop-file-install \
        --dir="${pkgdir}/usr/share/applications" \
        "${srcdir}/${pkgname}.desktop"
    for i in 16 32 48 64; do
        install -Dm644 "src/Gui/Icons/freecad-icon-${i}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/freecad.png"
    done
    install -Dm644 "src/Gui/Icons/freecad.svg" \
        "${pkgdir}/usr/share/icons/hicolor/scalable/apps/freecad.svg"

    # Mime info
    install -D -m644 "${srcdir}/freecad.xml" "${pkgdir}/usr/share/mime/packages/freecad.xml"
}
