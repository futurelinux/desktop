pkgname=tmux
pkgver=3.1_a
pkgrel=1
pkgdesc='A terminal multiplexer, intended as an alternative to GNU screen'
arch=('x86_64')
url='https://github.com/tmux/tmux/wiki'
license=('BSD')
depends=('ncurses' 'libevent' 'libutempter')
source=("https://github.com/tmux/tmux/releases/download/${pkgver/_/}/tmux-${pkgver/_/}.tar.gz"
        LICENSE)
sha256sums=('10687cbb02082b8b9e076cf122f1b783acc2157be73021b4bedb47e958f4e484'
            'b5de80619e4884ced2dfe0a96020e85dcfb715a831ecdfdd7ce8c97b5a6ff2cc')

build() {
    cd "$srcdir/$pkgname-${pkgver/_/}"
    ./configure --prefix=/usr --enable-utempter
    make
}

package() {
    cd "$srcdir/$pkgname-${pkgver/_/}"
    make DESTDIR="$pkgdir" install
    
    install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/tmux/LICENSE"
}
