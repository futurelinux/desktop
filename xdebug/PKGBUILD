pkgname=xdebug
pkgver=2.7.0
pkgrel=1
pkgdesc="PHP debugging extension"
arch=('x86_64')
url="https://xdebug.org/"
license=('GPL')
depends=('php' 're2c')
install="${pkgname}.install"
backup=('etc/php/conf.d/xdebug.ini')
categories=('programming')
source=("https://xdebug.org/files/$pkgname-${pkgver/rc/RC}.tgz"
	'xdebug.ini')
sha256sums=('e896da91ce0373f5fd8f4ca392c68da8593932ad51b2ec5eb3ee032b50d4b2d6'
            '4f8a29e379a16d68a29745c05f4ec5e3624492407482a9b8923ca91c1e6272e1')

build() {
  local PHPVER=`php -r 'echo phpversion();'`

  cd $srcdir/$pkgname-$pkgver
  phpize
  ./configure --prefix=/usr --enable-xdebug
  make

  cd debugclient
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/debugclient
  install -D -m 755 debugclient "$pkgdir"/usr/bin/debugclient

  cd $srcdir/$pkgname-$pkgver
  make INSTALL_ROOT=$pkgdir install
  install -D -m 644 $srcdir/xdebug.ini $pkgdir/etc/php/conf.d/xdebug.ini
}
