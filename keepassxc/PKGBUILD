pkgname=keepassxc
pkgver=2.4.0
pkgrel=2
pkgdesc="KeePassXC is a cross platform community driven port of the windows application Keepass Password Safe"
arch=('x86_64')
url="https://keepassxc.org/"
license=('GPL2')
screenshot="http://i1-win.softpedia-static.com/screenshots/KeePassXC_1.png"
depends=('hicolor-icon-theme' 'libxtst' 'qt5-x11extras' 'libgcrypt' 'curl' 'libsodium>=1.0.12' 'yubikey-personalization' 'argon2' 'qt5-svg' 'qrencode') 
makedepends=('cmake' 'qt5-tools')
provides=('keepassx' 'keepassx-reboot')
conflicts=('keepassx-reboot')
replaces=('keepassx-reboot')
source=("https://github.com/keepassxreboot/$pkgname/releases/download/$pkgver/$pkgname-$pkgver-src.tar.xz")
sha256sums=('081ff1a34da8e0e8db951b5d70824d7a2b3d299ed4ae54e6ddc1e3ac176b95a8')

prepare() {
    cd $pkgname-$pkgver
    mkdir -p build
}

build() {
    cd $pkgname-$pkgver/build
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_BINDIR=/usr/bin \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_VERBOSE_MAKEFILE=OFF \
        -DWITH_GUI_TESTS=OFF \
        -DWITH_XC_AUTOTYPE=ON \
        -DWITH_XC_NETWORKING=ON \
        -DWITH_XC_HTTP=ON \
        -DWITH_XC_BROWSER=ON \
        -DWITH_XC_YUBIKEY=ON \
        -DWITH_XC_KEESHARE=ON \
        -DCMAKE_BUILD_TYPE=Release ..
    make
}

#check() {
#    cd $pkgname-$pkgver/build
#    make test
#}

package() {
    cd $pkgname-$pkgver/build
    make DESTDIR=$pkgdir install
}

