pkgname=spring
_pkgname=springrts
pkgver=104.0
pkgrel=2
pkgdesc='A 3D real-time-strategy game engine.'
arch=('x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis' 'sdl2' 'libunwind'
         'libxcursor' 'curl' 'shared-mime-info' 'desktop-file-utils' 'libx11')
makedepends=('boost' 'cmake' 'zip' 'xz' 'p7zip' 'python2' 'java-environment' 'mesa')
optdepends=('python2: python-based bots'
            'java-runtime: java-based bots')
source=("https://downloads.sourceforge.net/sourceforge/$_pkgname/${pkgname}_${pkgver}_src.tar.lzma"
        fix-compile-error-because-of-new-openal-version.patch
        fix-reinterpret_cast-in-constexpr.patch)
sha256sums=('ceb0bf59f8aac14f2d844f920e898724913326cdf1adf3f10d88ecc317a3ec16'
            '06fb6052175b2f59ac7712c652010717301bcb678d97b065c7b816392f297bd0'
            '9c77440b895c616da207fe4e4bc4e6f4ff4226531fac7f2d5d318a56bad7ad9c')

prepare() {
  cd spring_$pkgver

  # https://springrts.com/mantis/view.php?id=5781
  # https://github.com/spring/spring/commit/6104061fe3
  patch -Np1 -i ../fix-reinterpret_cast-in-constexpr.patch
  
  patch -Np1 -i ../fix-compile-error-because-of-new-openal-version.patch
}

build() {
  bsdtar -xf ${pkgname}_${pkgver}_src.tar.lzma
  
  cd ${pkgname}_$pkgver

  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share/spring \
    -DCMAKE_SKIP_RPATH=YES
  make
}

package() {
  cd spring_${pkgver}

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/etc/spring"
  echo '$HOME/.local/share/spring' > "$pkgdir/etc/spring/datadir"
}
