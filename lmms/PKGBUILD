pkgname=lmms
pkgver=1.2.1
pkgrel=1
pkgdesc='The Linux MultiMedia Studio.'
url='http://lmms.sourceforge.net'
arch=('x86_64')
license=('GPL')
depends=('alsa-lib' 'fftw' 'glib2' 'qt5-base' 'qt5-x11extras' 'jack' 'libsamplerate' 'fluidsynth' 'libpng'
         'libvorbis' 'libxft' 'libxinerama' 'shared-mime-info' 'fltk' 'gcc-libs' 'glibc' 'lame' 'libogg'
         'portaudio' 'libpulse' 'libsndfile' 'libvorbis' 'libx11' 'stk' 'sdl' 'libgig')
optdepends=('wine: VST support (experimental)'
            'fftw: SpectrumAnalyzer plugin'
            'pulseaudio: PulseAudio output' )
makedepends=('cmake' 'ladspa' 'doxygen' 'freetype2' 'extra-cmake-modules' 'wine' 'qt5-tools' 'carla')
categories=('multimedia')
install="lmms.install"
source=("https://github.com/${pkgname}/${pkgname}/releases/download/v${pkgver}/${pkgname}_${pkgver}.tar.xz")
md5sums=('4fcb8daf2c844015c8c17e628e4713e6')

prepare() {
  cd $pkgname-$pkgver

  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake
}

build() {
  cd $pkgname-$pkgver
  export LDFLAGS="${LDFLAGS}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DWANT_QT5=ON \
        -DWANT_SOUNDIO=OFF \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_MODULE_PATH=/usr/share/ECM/find-modules \
        -DCMAKE_PREFIX_PATH=/usr/include/wine/windows \
        -DWINE_INCLUDE_DIR=/usr/include/wine/windows \
        -DWINE_LIBRARY=/usr/lib32/wine \
        -DOpenGL_GL_PREFERENCE=GLVND 
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
