pkgname=supertuxkart
pkgver=1.0
pkgrel=2
pkgdesc="Kart racing game featuring Tux and friends."
url="http://supertuxkart.sourceforge.net/"
arch=('x86_64')
license=('GPL3' 'GPL2' 'CCPL-by' 'CCPL-by-sa' 'custom:Public Domain')
depends=('bzip2' 'curl' 'enet' 'fribidi' 'freeglut' 'libgl' 'libjpeg' 'libogg' 'libopenglrecorder'
         'libpng' 'libvorbis' 'openal' 'wiiuse' 'bluez-libs' 'glu' 'xorg-xrandr')
makedepends=('cmake' 'subversion' 'mesa' 'imagemagick' 'setconf')
conflicts=('supertuxkart-svn')
categories=('games')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-src.tar.xz")
sha1sums=('cd270c851c5b15fb69c6e78e9824d87187c1f801')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  _fn="data/${pkgname}.desktop"
  setconf "$_fn" Exec "$pkgname"
  setconf "$_fn" TryExec "$pkgname"
  setconf "$_fn" Icon "$pkgname"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DIRRLICHT_DIR="$srcdir/irrlicht" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-lpthread -lm -ldl $CXXFLAGS -std=gnu++98" \
    -DUSE_SYSTEM_WIIUSE=ON \
    ..
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  
  make DESTDIR="${pkgdir}" install
}
