pkgname=supertuxkart
pkgver=1.1
pkgrel=2
pkgdesc="Kart racing game featuring Tux and friends."
url="http://supertuxkart.sourceforge.net/"
arch=('x86_64')
license=('GPL3' 'GPL2' 'CCPL-by' 'CCPL-by-sa' 'custom:Public Domain')
depends=('openal' 'libvorbis' 'fribidi' 'curl' 'bluez-libs' 'libxrandr' 'glu' 'wayland')
makedepends=('cmake' 'subversion' 'mesa' 'imagemagick' 'setconf' 'mesa-libgl' 'libxkbcommon')
conflicts=('supertuxkart-svn')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-src.tar.xz")
sha1sums=('68be26f4b15d30dede26563cbcc6de999036c5e0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-src"

  _fn="data/${pkgname}.desktop"
  setconf "$_fn" Exec "$pkgname"
  setconf "$_fn" TryExec "$pkgname"
  setconf "$_fn" Icon "$pkgname"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_RECORDER=0

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-src/build"
  
  make DESTDIR="${pkgdir}" install
  
  # Why is this even in here?
  rm -r "$pkgdir"/usr/lib
  rm -r "$pkgdir"/usr/include
}
