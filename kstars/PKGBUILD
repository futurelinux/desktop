# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdeedu-kstars/

pkgname=kstars
pkgver=3.5.0
pkgrel=1
epoch=1
pkgdesc="Desktop planetarium"
url="https://kde.org/applications/education/kstars/"
arch=('x86_64')
screenshot='https://www.kde.org/images/screenshots/kstars.png'
license=('GPL' 'LGPL' 'FDL')
depends=('stellarsolver' 'libraw' 'kplotting' 'knewstuff' 'knotifyconfig' 'qt5-datavis3d' 'libindi' 'qt5-quickcontrols2'
         'qt5-websockets' 'qtkeychain' 'breeze-icons' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python3' 'kdoctools' 'eigen3' 'xplanet')
optdepends=('xplanet: XPlanet support')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeedu')
replaces=('kdeedu-kstars')
conflicts=('kdeedu-kstars')
options=('debug')
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a1aa6e859b90bedcf88664c13e0cdfef6407aca1ee6eef608310d8a5caa9f33a'
            'SKIP')
validpgpkeys=(259D9FCEE9175351965633696D9CE2AEE028C4F3) # Jasem Mutlaq <mutlaqja@ikarustech.com>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOpenGL_GL_PREFERENCE=GLVND
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
