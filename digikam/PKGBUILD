pkgname=digikam
_pkgver=6.4.0
pkgver=${_pkgver//-/_}
pkgrel=1
epoch=1
pkgdesc="Digital photo management application for KDE"
url="https://www.digikam.org/"
arch=('x86_64')
license=('GPL')
options=("debug")
depends=('lensfun' 'opencv' 'akonadi-contacts' 'knotifyconfig' 'libksane' 'kfilemetadata'
         'qtav' 'marble' 'threadweaver' 'kcalcore' 'qt5-xmlpatterns' 'imagemagick' 'exiv2' 'qt5-webengine')
makedepends=('extra-cmake-modules' 'doxygen' 'eigen3' 'boost' 'kdoctools' 'libkvkontakte' 'jasper')
optdepends=('qt5-imageformats: support for additional image formats (WEBP, TIFF)'
	    'hugin: panorama tool'
            'libkvkontakte: VKontakte plugin')
source=("https://download.kde.org/stable/${pkgname}/${_pkgver}/${pkgname}-${_pkgver}.tar.xz"{,.sig})
sha1sums=('6f067b31a2109f82b2e78c89fc84bd03eab30674'
          'SKIP')
validpgpkeys=(D1CF2444A7858C5F2FB095B74A77747BC2386E50) # digiKam.org (digiKam project) <digikamdeveloper@gmail.com>

prepare() {
  mkdir -p build
}

build() {

  cd build

  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_TESTING=OFF \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_APPSTYLES=ON \
    -DENABLE_QWEBENGINE=ON \
    -DOpenGL_GL_PREFERENCE=GLVND
  make
}

package() {
  cd build/core
  make DESTDIR="$pkgdir" install
  cd $srcdir/build/po
  make DESTDIR="$pkgdir" install
}
