pkgname=flacon
pkgver=5.4.0
pkgrel=1
pkgdesc="Tool to extract individual tracks from one big audio file."
arch=('x86_64')
url="https://flacon.github.io/"
license=('LGPL2.1')
depends=('hicolor-icon-theme' 'qt5-base' 'uchardet' 'ffmpeg')
makedepends=('cmake' 'icu' 'qt5-tools')
optdepends=('flac: for encoding and decoding FLAC files'
            'lame: for MP3 support'
            'mp3gain: for MP3 Replay Gain support'
            'vorbis-tools: for OGG support'
            'wavpack: for WavPack support')
categories=('multimedia')
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('000eacf4528906dfe0adc1c21aedcb57bf4300e56e37150173d3a23acc8fe19f')

build() {
  cd $pkgname-$pkgver
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver/build
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m 644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  make DESTDIR=$pkgdir install
}
