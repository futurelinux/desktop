# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami-gallery
pkgver=19.08.3
pkgrel=1
pkgdesc="Gallery application built using Kirigami"
url="https://www.kde.org/products/kirigami/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kirigami2 qt5-svg)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4ceeb854aa95902effb99cb2e5956a2f237ac3880f5b5d8e22849ed7887288df'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
