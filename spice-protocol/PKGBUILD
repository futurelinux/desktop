# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgname=spice-protocol
pkgver=0.14.0
pkgrel=1
pkgdesc="Headers for SPICE protocol"
arch=(any)
url="http://spice-space.org"
license=('BSD' 'LGPL2.1')
makedepends=('meson')
source=(https://www.spice-space.org/download/releases/$pkgname-$pkgver.tar.bz2{,.sig})
sha1sums=('8958c5eb11c67ac213cc6d55a736e9aa51526137'
          'SKIP')
validpgpkeys=('206D3B352F566F3B0E6572E997D9123DE37A484F') # Victor Toso de Carvalho <me@victortoso.com>

build() {
  mkdir build
  cd "$srcdir/$pkgname-$pkgver"
  chakra-meson ../build
  ninja -v -C ../build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir/" ninja -C ../build install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
