pkgname=supertux
pkgver=0.6.1.1
pkgrel=1
pkgdesc="A classic 2D jump'n run sidescroller game in a style similar to the original SuperMario games"
arch=('x86_64')
url="https://github.com/supertux/supertux"
license=('GPL')
depends=('sdl2_image' 'curl' 'openal' 'libvorbis' 'glew' 'freetype2')
makedepends=('cmake' 'boost' 'mesa' 'optipng')
source=(https://github.com/SuperTux/$pkgname/releases/download/v${pkgver}/SuperTux-v${pkgver}-Source.tar.gz)
sha1sums=('d00a25afe9db90985596ce67a4555529df411c54')

build() {
  cd "SuperTux-v${pkgver}-Source"

  cmake . \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DINSTALL_SUBDIR_BIN=bin \
      -DCMAKE_BUILD_TYPE=Release \
      -DUSE_SYSTEM_PHYSFS=0
  make
}

package() {
  cd "SuperTux-v${pkgver}-Source"

  make DESTDIR="${pkgdir}" install
}
