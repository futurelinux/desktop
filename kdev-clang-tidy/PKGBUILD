# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdev-clang-tidy
pkgver=0.3.3
pkgrel=1
pkgdesc="Clang-tidy analyzer plugin for KDevelop"
arch=(x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(kdevelop)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
provides=(kdevelop-clang-tidy)
conflicts=(kdevelop-clang-tidy)
replaces=(kdevelop-clang-tidy)
sha256sums=('414f28784e0a75ae4bfd4a892a1eb8d65116d355945a929587fb11c30967792f'
            'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6'
              '364EFA5F639572907D392999C64CF56B13CACE5D'
              'E191FD5BE6F46870F09E82B2024E7FB43D015474')
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdev-clang-tidy-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
