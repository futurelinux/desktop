

pkgname=akonadi-search
pkgver=19.08.2
pkgrel=1
pkgdesc="Libraries and daemons to implement searching in Akonadi"
url='https://projects.kde.org/akonadi-search'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=(xapian-core akonadi-mime kcontacts kcalcore krunner kcmutils)
makedepends=(extra-cmake-modules kdoctools boost python kdesignerplugin)
options=('debug')
replaces=('baloo4-akonadi')
provides=('baloo4-akonadi')
conflicts=('baloo4-akonadi')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('75c9713b84a03c60a68ff36652decbf57f4f56a0fb39579f53e7ed80a5ee8525'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}

